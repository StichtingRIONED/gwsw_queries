# Aansluiting_Leiding.rq (20180918-20220405)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX : <http://data.gwsw.nl/1.5/totaal/>
PREFIX : <{{&version}}>

SELECT ?Stelsel ?stelseltype ?naam ?type ?Lijn ?sluitAanOp ?Begindatum ?MateriaalLeiding ?VormLeiding ?BreedteLeiding ?HoogteLeiding ?LengteLeiding ?BobBeginpuntLeiding ?datumInwinningBobBegin ?wijzeInwinningBobBegin ?BobEindpuntLeiding ?datumInwinningBobEind ?wijzeInwinningBobEind 

WHERE
{ 
   {?lei rdf:type :Aansluitleiding .}
   UNION
   {?lei rdf:type :Goot .}
   
   ?lei sesame:directType ?type .
    
   FILTER (!(isBlank(?lei)))
   FILTER (!(isBlank(?type)))
    
   ?lei rdfs:label ?naam .

   optional
   {   
      ?lei :isPartOf ?aansl .
	  ?aansl rdf:type :Aansluiting ; # Altijd deel van Aansluiting (20200501)
        :isPartOf 
        [ rdf:type :Stelsel ;
            rdfs:label ?Stelsel ;
            sesame:directType ?stelseltype ;
        ] .
   
      optional # Inlaat binnen aansluiting sluit aan op put/leiding
      {
        ?inlaat rdf:type :Inlaat .
        ?inlaat :isPartOf ?aansl .
        ?inlaat :isPartOf ?obj .

        { ?obj rdf:type :Rioolput . }
        UNION
        { ?obj rdf:type :Leiding . }
        
        ?obj sesame:directType ?typeObj .

        FILTER (!(isBlank(?obj)))
        FILTER (!(isBlank(?typeObj)))

        #?obj rdfs:label ?_lab .
        BIND(STR(?obj) as ?sluitAanOp) # de URI van de put/leiding
      }
 	  #BIND(CONCAT(?StelselLab, " (zie ", str(?StelselUri), " )") as ?Stelsel)
   }
   ?lei :hasAspect ?ori .
   ?ori rdf:type :Leidingorientatie .

   ?ori :hasAspect # geometrie moet er zijn vanaf GWSW 1.4
   [ 
      rdf:type :Lijn ;
      :hasValue ?Lijn ;
   ] .
   optional # aansluiting begin. Hou synchroon: Beheer_Leiding
   {
        ?ori :hasPart ?bpnt .
        ?bpnt rdf:type :BeginpuntLeiding .
		
        optional {
            ?bpnt :hasAspect ?bobB .
            ?bobB rdf:type :BobBeginpuntLeiding ;
                  :hasValue ?BobBeginpuntLeiding .

            optional {?bobB :hasAspect
            [
                rdf:type :Inwinning ;
                :hasAspect [
                rdf:type :DatumInwinning ;
                :hasValue ?datumInwinningBobBegin ;
                ] ;
            ] . }
            optional {?bobB :hasAspect
            [
                rdf:type :Inwinning ;
                :hasAspect [
                rdf:type :WijzeVanInwinning ;
                :hasReference ?wijzeInwinningBobBegin ;
                ] ;
            ] . }
        }
   }
   optional # aansluiting eind. Hou synchroon: Beheer_Leiding
   {
        ?ori :hasPart ?epnt .
        ?epnt rdf:type :EindpuntLeiding .
		
        optional {
            ?epnt :hasAspect ?bobE .
            ?bobE rdf:type :BobEindpuntLeiding ;
                  :hasValue ?BobEindpuntLeiding .

            optional {?bobE :hasAspect
            [
                rdf:type :Inwinning ;
                :hasAspect [
                rdf:type :DatumInwinning ;
                :hasValue ?datumInwinningBobEind ;
                ] ;
            ] . }
            optional {?bobE :hasAspect
            [
                rdf:type :Inwinning ;
                :hasAspect [
                rdf:type :WijzeVanInwinning ;
                :hasReference ?wijzeInwinningBobEind ;
                ] ;
            ] . }
        }
   }
   optional {?lei :hasAspect 
   [
       rdf:type :Begindatum ;
       :hasValue ?Begindatum ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :MateriaalLeiding ;
       :hasReference ?MateriaalLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :VormLeiding ;
       :hasReference ?VormLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :DiameterLeiding ;
       :hasValue ?BreedteLeiding ; # Diameter als breedte vermelden
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :BreedteLeiding ;
       :hasValue ?BreedteLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :HoogteLeiding ;
       :hasValue ?HoogteLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :LengteLeiding ;
       :hasValue ?LengteLeiding ;
   ] .}

}
