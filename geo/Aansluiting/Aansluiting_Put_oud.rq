PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX : <{{&version}}>

SELECT ?Stelsel ?naam ?type ?Punt ?Begindatum ?Maaiveldhoogte

WHERE
{ 
   {
   	  ?put rdf:type :Aansluitput ;
	       :hasAspect ?ori .
      ?ori rdf:type :Putorientatie ;
   }
   UNION
   {
	  {?put rdf:type :Hulpstuk .}
      UNION
	  {?put rdf:type :Inlaat .} # Nog corrigeren in GWSW: Inlaat heeft geen orientatie (20190316)
      
	  ?put :hasAspect ?ori .
      ?ori rdf:type :Hulpstukorientatie .
   }
   ?put sesame:directType ?type .
   ?ori :hasAspect
   [ 
        rdf:type :Punt ;
        :hasValue ?Punt ;
   ] .
    
   FILTER (!(isBlank(?put)))
   FILTER (!(isBlank(?type)))
    
   ?put rdfs:label ?naam .  
    
   optional {?ori :hasConnection
   [
     rdf:type :Maaiveldorientatie ;
     :hasAspect
     [
	 rdf:type :Maaiveldhoogte ;
	 :hasValue ?Maaiveldhoogte ;
     ];
   ] .}
   optional {?put :isPartOf 
      [ rdf:type :Stelsel ;
        rdfs:label ?Stelsel ;
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :Begindatum ;
       :hasValue ?Begindatum ;
   ] .}
}
