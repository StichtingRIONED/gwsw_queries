# GWSW/Geo-thema Waterschap: Constructieonderdeel (20230416)

# Filter op:
# * heeft geometrie gwsw:Punt
# * is (super)type gwsw:Constructieonderdeel (kan veel zijn)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX gwsw: <http://data.gwsw.nl/1.6/totaal/>
PREFIX gwsw: <{{&version}}>

SELECT ?Punt ?type ?uri_leiding ?Begindatum ?Einddatum ?code ?naam

WHERE
{
  BIND (str(iri(gwsw:)) AS ?aft) # de prefix, voor optioneel inkorten IRI

  ?obj gwsw:hasAspect ?ori .
  ?ori rdf:type gwsw:TopologischElement .
  FILTER (!(isBlank(?obj)))
  ?obj rdf:type gwsw:Constructieonderdeel .
  ?obj sesame:directType ?type .

  ?ori gwsw:hasAspect
  [ 
    rdf:type gwsw:Punt ;
    gwsw:hasValue ?Punt ;
  ] .
   optional { ?obj rdfs:label ?naam . } # niet meer verplicht (alleen geometrie moet kloppen) (20220518)

   optional { # Deel van een leiding?
    ?obj gwsw:isPartOf ?uri_leiding .
    ?uri_leiding rdf:type gwsw:Leiding .
   }
   optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:Begindatum ;
       gwsw:hasValue ?Begindatum ;
   ] .}
   optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:Einddatum ;
       gwsw:hasValue ?Einddatum ;
   ] .}
   optional {?obj rdfs:comment ?code. }
}