# Geoserver: Hyd_Verbinding.spq (20170910-20170930)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX : <http://data.gwsw.nl/1.3.1/totaal/>

SELECT ?Stelsel ?naam ?type ?Lijn ?beginpunt ?eindpunt ?BobBeginpuntLeiding ?BobEindpuntLeiding ?MateriaalLeiding ?VormLeiding ?BreedteLeiding ?HoogteLeiding ?LengteLeiding ?AantalWoningen ?Aantal_ieBedrijven ?Aantal_ieRecreatie ?AfvoerendOppervlak ?LateraalDebietDWA ?LateraalDebietHWA ?LateraalAfvoerendOppervlak

WHERE
{ 
   ?lei rdf:type :Leiding .
   ?lei sesame:directType ?type .
    
   FILTER (!(isBlank(?lei)))
   FILTER (!(isBlank(?type)))
    
   ?lei rdfs:label ?naam .

   optional
   {   
      ?lei :isPartOf 
      [ rdf:type :Stelsel ;
        rdfs:label ?Stelsel ;
      ] .
   }
   ?lei :hasAspect ?ori .
   ?ori rdf:type :Leidingorientatie .

    optional
    {
        ?ori :hasAspect
        [ 
            rdf:type :Lijn ;
            :hasValue ?geo1 ;
        ]
    }
    optional # aansluiting begin. Hou synchroon: hyd_verbinding, mds_leiding
    {
        ?ori :hasPart ?bpnt .
        ?bpnt rdf:type :BeginpuntLeiding .
		
        optional {?bpnt :hasAspect
        [ 
           rdf:type :BobBeginpuntLeiding ;
           :hasValue ?BobBeginpuntLeiding ;
        ] .}
		
        ?bpnt :hasConnection ?bconn . # Let op: niet optioneel, anders loopt query slecht
		
		optional
		{
			?bconn :hasAspect
			[ 
				rdf:type :Punt ;
				:hasValue ?bgeo ;
            ] .
		}
			
		# Bepaal naam van de aansluiting
		
 		{ ?bconn :isAspectOf ?bconn1 . }
        union
        { ?bconn :isPartOf [ :isAspectOf ?bconn1 ; ] . } # rechtstreekse aansluiting op pomp
            
        ?bconn1 rdfs:label ?beginpunt1 .
		
        optional # toegevoegde naam?
		{
			?bconn1 :isPartOf ?bconn2 . # deel van put / bouwwerk
            filter not exists {?bconn2 rdf:type :Stelsel .} # alles behalve Stelsel-types
			?bconn2 rdfs:label ?beginpunt2 .
		}
        bind (if(bound(?beginpunt1) && bound(?beginpunt2), concat(str(?beginpunt2), "/", str(?beginpunt1)), if (bound(?beginpunt1), ?beginpunt1, "")) as ?beginpunt)
	}
    optional # aansluiting eind. Hou synchroon: hyd_verbinding, mds_leiding
    {
		?ori :hasPart ?epnt .
		?epnt rdf:type :EindpuntLeiding .
		
        optional {?epnt :hasAspect
        [ 
           rdf:type :BobEindpuntLeiding ;
           :hasValue ?BobEindpuntLeiding ;
        ] .}
		
      	?epnt :hasConnection ?econn . # Let op: niet optioneel, anders loopt query slecht
		
		optional
        {
            ?econn :hasAspect
            [ 
                rdf:type :Punt ;
                :hasValue ?egeo ;
            ] . 
 		}
		
		# Bepaal naam van de aansluiting
            
        { ?econn :isAspectOf ?econn1 . }
        union
        { ?econn :isPartOf [ :isAspectOf ?econn1 ; ] . } # rechtstreekse aansluiting op pomp
            
      	?econn1 rdfs:label ?eindpunt1 .

        optional # toegevoegde naam?
		{
			?econn1 :isPartOf ?econn2 . # deel van put / bouwwerk
            filter not exists {?econn2 rdf:type :Stelsel .} # alles behalve Stelsel-types
			?econn2 rdfs:label ?eindpunt2 .
		}
        bind (if(bound(?eindpunt1) && bound(?eindpunt2), concat(str(?eindpunt2), "/", str(?eindpunt1)), if (bound(?eindpunt1), ?eindpunt1, "")) as ?eindpunt)
    }
	optional # maak zonodig geometrie. Hou synchroon: hyd_verbinding, mds_leiding
    {
        filter (!bound(?geo1) && Bound(?bgeo) && Bound(?egeo))

        bind (concat(strafter(strbefore(str(?bgeo), "</gml:pos>"), "<gml:pos>"), " ") as ?bxy) # lees inhoud gml:pos, voeg spatie toe voor zoekwerk
        bind (strbefore(?bxy, " ") as ?bx)
        bind (strbefore(substr(?bxy, strlen(?bx) + 2), " ") as ?by) # substr = 1-based

        bind (concat(strafter(strbefore(str(?egeo), "</gml:pos>"), "<gml:pos>"), " ") as ?exy) # lees inhoud gml:pos, voeg spatie toe voor zoekwerk
        bind (strbefore(?exy, " ") as ?ex)
        bind (strbefore(substr(?exy, strlen(?ex) + 2), " ") as ?ey) # substr = 1-based

        bind (concat("<gml:LineString xmlns:gml=\"http://www.opengis.net/gml\"><gml:posList srsDimension=\"2\">", ?bx, " ", ?by, " ", ?ex, " ", ?ey, "</gml:posList></gml:LineString>") as ?geo2)
    }
    BIND (COALESCE (?geo1, ?geo2) as ?Lijn) # de eerst geldende wordt ?Lijn
	
   FILTER (BOUND(?Lijn)) # Geometrie moet er zijn, ivm optioneel toegevoegde limit (count parameter in GWSW Geoserver)
   
   optional {?lei :hasAspect 
   [
       rdf:type :MateriaalLeiding ;
       :hasReference ?MateriaalLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :VormLeiding ;
       :hasReference ?VormLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :DiameterLeiding ;
       :hasValue ?BreedteLeiding ; # Diameter als breedte vermelden
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :BreedteLeiding ;
       :hasValue ?BreedteLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :HoogteLeiding ;
       :hasValue ?HoogteLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :LengteLeiding ;
       :hasValue ?LengteLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :AantalWoningen ;
       :hasValue ?AantalWoningen ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :Aantal_ieBedrijven ;
       :hasValue ?Aantal_ieBedrijven ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :Aantal_ieRecreatie ;
       :hasValue ?Aantal_ieRecreatie ;
   ] .}
    
   optional
   {
        SELECT ?lei (sum(?ao) as ?AfvoerendOppervlak) # sommeer de types ao
        WHERE
        {
           optional {?lei :hasAspect 
           [
               rdf:type :AfvoerendOppervlak ;
               :hasValue ?ao ;
           ] .}
        }
        GROUP BY ?lei
    }
   optional {?lei :hasAspect 
   [
       rdf:type :LateraalDebietDWA ;
       :hasValue ?LateraalDebietDWA ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :LateraalDebietHWA ;
       :hasValue ?LateraalDebietHWA ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :LateraalAfvoerendOppervlak ;
       :hasValue ?LateraalAfvoerendOppervlak ;
   ] .}

}
