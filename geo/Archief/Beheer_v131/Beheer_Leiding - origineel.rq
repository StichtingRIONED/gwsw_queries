# Mds_Leiding.spq

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX : <http://data.gwsw.nl/1.3.1/totaal/>

SELECT distinct ?Stelsel ?naam ?type ?Lijn ?beginpunt ?eindpunt ?Begindatum ?Einddatum ?BobBeginpuntLeiding ?BobEindpuntLeiding ?MateriaalLeiding ?VormLeiding ?BreedteLeiding ?HoogteLeiding ?LengteLeiding ?Wanddikte ?Verbindingstype ?Fundering ?DatumInspectie ?DatumReiniging ?WIONThema ?AantalWoningen ?Aantal_ieBedrijven ?Aantal_ieRecreatie ?AfvoerendOppervlak

WHERE
{ 
   ?lei rdf:type :Leiding .
   ?lei sesame:directType ?type .
    
   FILTER (!(isBlank(?lei)))
   FILTER (!(isBlank(?type)))
    
   ?lei rdfs:label ?naam .

   optional
   {   
      ?lei :isPartOf 
      [ rdf:type :Stelsel ;
        rdfs:label ?Stelsel ;
      ] .
   }
   ?lei :hasAspect ?ori .
   ?ori rdf:type :Leidingorientatie .

    optional
    {
        ?ori :hasAspect
        [ 
            rdf:type :Lijn ;
            :hasValue ?geo1 ;
        ]
    }
    optional
    {
        ?ori :hasPart ?bpnt .
        ?bpnt rdf:type :BeginpuntLeiding .
        ?ori :hasPart ?epnt .
        ?epnt rdf:type :EindpuntLeiding .
		
        optional
        {
            ?bpnt :hasConnection ?bconn .
            
            { ?bconn :isAspectOf ?bconn1 . }
            union
            { ?bconn :isPartOf [ :isAspectOf ?bconn1 ; ] . } # rechtstreekse aansluiting op pomp
            
        	?bconn1 rdfs:label ?beginpunt1 .
        }
        optional
		{
			?bconn1 :isPartOf ?bconn2 . # deel van put / bouwwerk
            filter not exists {?bconn2 rdf:type :Stelsel .} # alles behalve Stelsel-types
			?bconn2 rdfs:label ?beginpunt2 .
		}
		optional
        {
            ?bconn :hasAspect
              [ 
                rdf:type :Punt ;
                :hasValue ?bgeo ;
              ] . 
 		}
        optional {?bpnt :hasAspect
        [ 
           rdf:type :BobBeginpuntLeiding ;
           :hasValue ?BobBeginpuntLeiding ;
        ] .}
        bind (if(bound(?beginpunt1) && bound(?beginpunt2), concat(str(?beginpunt2), "/", str(?beginpunt1)), if (bound(?beginpunt1), ?beginpunt1, "")) as ?beginpunt)
        
        optional
        {
       		?epnt :hasConnection ?econn .
            
            { ?econn :isAspectOf ?econn1 . }
            union
            { ?econn :isPartOf [ :isAspectOf ?econn1 ; ] . } # rechtstreekse aansluiting op pomp
            
        	?econn1 rdfs:label ?eindpunt1 .
        }
		optional
		{
			?econn1 :isPartOf ?econn2 . # deel van put / bouwwerk
            filter not exists {?econn2 rdf:type :Stelsel .}
			?econn2 rdfs:label ?eindpunt2 .
		}
		optional
        {
            ?econn :hasAspect
              [ 
                rdf:type :Punt ;
                :hasValue ?egeo ;
              ] . 
 		}
        optional {?epnt :hasAspect
        [ 
           rdf:type :BobEindpuntLeiding ;
           :hasValue ?BobEindpuntLeiding ;
        ] .}
        bind (if(bound(?eindpunt1) && bound(?eindpunt2), concat(str(?eindpunt2), "/", str(?eindpunt1)), if (bound(?eindpunt1), ?eindpunt1, "")) as ?eindpunt)
    }
	optional
    {
        filter (!bound(?geo1) && Bound(?bgeo) && Bound(?egeo))

        bind (concat(strafter(strbefore(str(?bgeo), "</gml:pos>"), "<gml:pos>"), " ") as ?bxy) # lees inhoud gml:pos, voeg spatie toe voor zoekwerk
        bind (strbefore(?bxy, " ") as ?bx)
        bind (strbefore(substr(?bxy, strlen(?bx) + 2), " ") as ?by) # substr = 1-based

        bind (concat(strafter(strbefore(str(?egeo), "</gml:pos>"), "<gml:pos>"), " ") as ?exy) # lees inhoud gml:pos, voeg spatie toe voor zoekwerk
        bind (strbefore(?exy, " ") as ?ex)
        bind (strbefore(substr(?exy, strlen(?ex) + 2), " ") as ?ey) # substr = 1-based

        bind (concat("<gml:LineString xmlns:gml=\"http://www.opengis.net/gml\"><gml:posList srsDimension=\"2\">", ?bx, " ", ?by, " ", ?ex, " ", ?ey, "</gml:posList></gml:LineString>") as ?geo2)
    }
    BIND (COALESCE (?geo1, ?geo2) as ?Lijn) # de eerst geldende wordt ?Lijn

   optional {?lei :hasAspect 
   [
       rdf:type :Begindatum ;
       :hasValue ?Begindatum ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :MateriaalLeiding ;
       :hasReference ?MateriaalLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :VormLeiding ;
       :hasReference ?VormLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :DiameterLeiding ;
       :hasValue ?BreedteLeiding ; # Diameter als breedte vermelden
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :BreedteLeiding ;
       :hasValue ?BreedteLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :HoogteLeiding ;
       :hasValue ?HoogteLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :LengteLeiding ;
       :hasValue ?LengteLeiding ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :Wanddikte ;
       :hasValue ?Wanddikte ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :Verbindingstype ;
       :hasReference ?Verbindingstype ;
   ] .}
   optional {?lei :hasPart 
   [
       rdf:type :Fundering ;
       sesame:directType ?Fundering ;
   ] .}
   optional {?lei :isInputOf 
   [
     rdf:type :Inspecteren ;
     :hasAspect
     [
	rdf:type :DatumMaatregel ;
	:hasValue ?DatumInspectie ;
     ];
   ] .}
   optional {?lei :isInputOf 
   [
     rdf:type :Reinigen ;
     :hasAspect
     [
	rdf:type :DatumMaatregel ;
	:hasValue ?DatumReiniging ;
     ];
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :WIONThema ;
       :hasReference ?WIONThema ;
   ] .}
   # De belastingen er afgehaald, query werd te lang (via post doen?)
   # Zie hyd_verbindingen voor de ontbrekende syntax
      optional {?lei :hasAspect 
   [
       rdf:type :AantalWoningen ;
       :hasValue ?AantalWoningen ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :Aantal_ieBedrijven ;
       :hasValue ?Aantal_ieBedrijven ;
   ] .}
   optional {?lei :hasAspect 
   [
       rdf:type :Aantal_ieRecreatie ;
       :hasValue ?Aantal_ieRecreatie ;
   ] .}
    
   optional
   {
        SELECT ?lei (sum(?ao) as ?AfvoerendOppervlak) # sommeer de types ao
        WHERE
        {
           optional {?lei :hasAspect 
           [
               rdf:type :AfvoerendOppervlak ;
               :hasValue ?ao ;
           ] .}
        }
        GROUP BY ?lei
   }
}
