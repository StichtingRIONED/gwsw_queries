PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX : <http://data.gwsw.nl/1.3.1/totaal/>

SELECT distinct ?Stelsel ?Naam ?Type ?Geometrie

WHERE
{ 
   ?lei rdf:type :Leiding .
   ?lei sesame:directType ?Type .
    
   FILTER (!(isBlank(?lei)))
   FILTER (!(isBlank(?Type)))
    
   ?lei rdfs:label ?Naam .
    
    optional
    {
      ?lei :isPartOf 
      [ rdf:type :Stelsel ;
        rdfs:label ?Stelsel ;
      ] .
    }
  
    ?lei :hasAspect ?ori .
    ?ori rdf:type :Leidingorientatie .
   
	# lees lei-geo, of gekoppelde put-geo
    # Maak zonodig gml:Linestring vanuit 2 x gml:Point

    optional
    {
        ?ori :hasAspect
        [ 
            rdf:type :Lijn ;
            :hasValue ?geo1 ;
        ]
    }
    optional
    {
        filter (!bound(?geo1))
        
        ?ori :hasPart ?bpnt .
        ?bpnt rdf:type :BeginpuntLeiding .
        ?ori :hasPart ?epnt .
        ?epnt rdf:type :EindpuntLeiding .
       
        ?bpnt :hasConnection
        [ 
          :hasAspect
          [ 
            rdf:type :Punt ;
            :hasValue ?bgeo ;
          ]
        ] .
        ?epnt :hasConnection
        [ 
          :hasAspect
          [ 
            rdf:type :Punt ;
            :hasValue ?egeo ;
          ]
        ] .
        bind (concat(strafter(strbefore(str(?bgeo), "</gml:pos>"), "<gml:pos>"), " ") as ?bxy) # lees inhoud gml:pos, voeg spatie toe voor zoekwerk
        bind (strbefore(?bxy, " ") as ?bx)
        bind (strbefore(substr(?bxy, strlen(?bx) + 2), " ") as ?by) # substr = 1-based

        bind (concat(strafter(strbefore(str(?egeo), "</gml:pos>"), "<gml:pos>"), " ") as ?exy) # lees inhoud gml:pos, voeg spatie toe voor zoekwerk
        bind (strbefore(?exy, " ") as ?ex)
        bind (strbefore(substr(?exy, strlen(?ex) + 2), " ") as ?ey) # substr = 1-based

        bind (concat("<gml:LineString xmlns:gml=\"http://www.opengis.net/gml\"><gml:posList srsDimension=\"2\">", ?bx, " ", ?by, " ", ?ex, " ", ?ey, "</gml:posList></gml:LineString>") as ?geo2)
    }
    BIND (COALESCE (?geo1, ?geo2) as ?Geometrie) # de eerst geldende wordt ?Geometrie
	
    FILTER (BOUND(?Geometrie)) # Geometrie moet er zijn, ivm optioneel toegevoegde limit (count parameter in GWSW Geoserver)
}
