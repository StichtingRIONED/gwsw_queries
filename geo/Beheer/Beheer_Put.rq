# Beheer_Put.rq

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX : <http://data.gwsw.nl/1.5/totaal/>
PREFIX : <{{&version}}>

SELECT ?Stelsel ?stelseltype ?naam ?uri ?type ?Punt ?Begindatum ?Einddatum ?Maaiveldhoogte ?datumInwinningMaaiveld ?wijzeInwinningMaaiveld ?MateriaalPut ?VormPut ?BreedtePut ?LengtePut ?HoogtePut ?Wanddikte ?Fundering ?DatumReiniging ?DatumInspectie ?AantalWoningen ?Aantal_ieBedrijven ?Aantal_ieRecreatie ?AfvoerendOppervlak ?BergendOppervlak

WHERE
{ 
   { ?put rdf:type :Rioolput . }
   
   # Via thema Aansluiting (20190403)
   #UNION
   #{ ?put rdf:type :Kolk . }
   
   ?put sesame:directType ?type .
    
   FILTER (!(isBlank(?put)))
   FILTER (!(isBlank(?type)))
    
   ?put rdfs:label ?naam .

   optional
   {   
      ?put :isPartOf 
      [ rdf:type :Stelsel ;
        rdfs:label ?Stelsel ;
        sesame:directType ?stelseltype ;
      ] .
   }
   ?put :hasAspect ?ori .
   ?ori rdf:type :Putorientatie ;
       :hasAspect
   [ 
       rdf:type :Punt ;
       :hasValue ?Punt ;
   ] .
   optional {?ori :hasConnection
     [
        rdf:type :Maaiveldorientatie ;
        :hasAspect ?maaiveld ;
     ] . 
     ?maaiveld  rdf:type :Maaiveldhoogte ;
                :hasValue ?Maaiveldhoogte .
     optional {?maaiveld :hasAspect
     [
        rdf:type :Inwinning ;
        :hasAspect [
        rdf:type :DatumInwinning ;
        :hasValue ?datumInwinningMaaiveld ;
        ] ;
     ] . }
     optional {?maaiveld :hasAspect
     [
        rdf:type :Inwinning ;
        :hasAspect [
        rdf:type :WijzeVanInwinning ;
        :hasReference ?wijzeInwinningMaaiveld ;
        ] ;
     ] . }
   }
   optional {?put :hasAspect 
   [
       rdf:type :Begindatum ;
       :hasValue ?Begindatum ;
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :Einddatum ;
       :hasValue ?Einddatum ;
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :MateriaalPut ;
       :hasReference ?MateriaalPut ;
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :VormPut ;
       :hasReference ?VormPut ;
   ] .}

   optional {?put :hasAspect 
   [
       rdf:type :DiameterPut ;
   # Werkt niet altijd goed? Opgelost via COALESCE (20230710)
   #      :hasValue ?BreedtePut ; # Diameter als breedte vermelden
       :hasValue ?dia ;  
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :BreedtePut ;
       :hasValue ?br ;
   ] .}
   BIND (COALESCE (?br, ?dia) as ?BreedtePut) # de eerst geldende doet mee (20230710)   
   
   optional {?put :hasAspect 
   [
       rdf:type :LengtePut ;
       :hasValue ?LengtePut ;
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :HoogtePut ;
       :hasValue ?HoogtePut ;
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :Wanddikte ;
       :hasValue ?Wanddikte ;
   ] .}
   optional {?put :hasPart 
   [
       rdf:type :Fundering ;
       sesame:directType ?Fundering ;
   ] .}
   optional {?put :isInputOf 
   [
     rdf:type :Inspecteren ;
     :hasAspect
     [
	rdf:type :DatumMaatregel ;
	:hasValue ?DatumInspectie ;
     ];
   ] .}
   optional {?put :isInputOf 
   [
     rdf:type :Reinigen ;
     :hasAspect
     [
	rdf:type :DatumMaatregel ;
	:hasValue ?DatumReiniging ;
     ];
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :AantalWoningen ;
       :hasValue ?AantalWoningen ;
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :Aantal_ieBedrijven ;
       :hasValue ?Aantal_ieBedrijven ;
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :Aantal_ieRecreatie ;
       :hasValue ?Aantal_ieRecreatie ;
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :AfvoerendOppervlak ;
       :hasValue ?AfvoerendOppervlak ;
   ] .}
   optional {?put :hasAspect 
   [
       rdf:type :BergendOppervlak ;
       :hasValue ?BergendOppervlak ;
   ] .}
   BIND (strafter(str(?put), "#") AS ?uri)
}
