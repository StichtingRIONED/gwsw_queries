# Geo-thema Default (20210521)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX gwsw: <http://data.gwsw.nl/1.6/totaal/>
PREFIX gwsw: <{{&version}}>

SELECT ?Stelsel ?stelseltype ?naam ?uri ?type ?Lijn ?beginpunt ?uriB ?eindpunt ?uriE ?Begindatum ?Einddatum ?BobBeginpuntLeiding ?BobEindpuntLeiding ?MateriaalLeiding ?VormLeiding ?BreedteLeiding ?HoogteLeiding ?LengteLeiding ?Wanddikte ?Verbindingstype ?Fundering ?DatumInspectie ?DatumReiniging ?WIONThema ?AantalWoningen ?Aantal_ieBedrijven ?Aantal_ieRecreatie ?AfvoerendOppervlak ?LateraalDebietDWA ?LateraalDebietHWA ?LateraalAfvoerendOppervlak
#SELECT ?Stelsel ?Naam ?Type ?TypeNaam ?Geometrie

WHERE
{ 
  {
    ?obj gwsw:hasAspect ?ori .
    ?ori rdf:type gwsw:TopologischElement . # lees lei-geo (vanaf versie 1.4 altijd aanwezig)
  }
  UNION
  {
    ?obj rdf:type gwsw:Afvoerrelatie . # ook de zelfstandige topologische elementen
    BIND (?obj as ?ori)
  }
  FILTER (!(isBlank(?obj)))
  ?obj sesame:directType ?type .
  FILTER (!(isBlank(?type)))

  ?ori gwsw:hasAspect
    [ 
        rdf:type gwsw:Lijn ;
        gwsw:hasValue ?Lijn ;
    ] .

  optional { ?obj rdfs:label ?naam . } # niet meer verplicht (alleen geometrie moet kloppen) (20220518)   
  
  optional
  {
    ?obj gwsw:isPartOf 
    [ rdf:type gwsw:Stelsel ;
      rdfs:label ?Stelsel ;
      sesame:directType ?stelseltype ;
    ] .
  }
  # Bijgewerkt: ?ori en ?lijn werden dubbel bepaald (20210521)
  #?obj gwsw:hasAspect ?ori .
  #?ori rdf:type gwsw:Leidingorientatie .
  #?ori gwsw:hasAspect # geometrie moet er zijn vanaf GWSW 1.4
  #[ 
  #  rdf:type gwsw:Lijn ;
  #  gwsw:hasValue ?Lijn ;
  #] .

  optional # aansluiting begin. Hou synchroon: hyd_verbinding, mds_leiding
  {
    ?ori gwsw:hasPart ?bpnt .
    ?bpnt rdf:type gwsw:BeginpuntLeiding .
    ?bpnt gwsw:hasConnection ?bconn . # Let op: niet optioneel, anders loopt query slecht

    optional {?bpnt gwsw:hasAspect
    [ 
        rdf:type gwsw:BobBeginpuntLeiding ;
        gwsw:hasValue ?BobBeginpuntLeiding ;
    ] .}

    # Hou het kort voor MDS (Delft liep vast op het vervolg)

    ?bconn gwsw:isAspectOf ?_uriB . # lei in de query-resultaten voor zoekwerk (20200708)
    ?_uriB rdfs:label ?beginpunt .
  }
  optional # aansluiting eind. Hou synchroon: hyd_verbinding, mds_leiding
  {
    ?ori gwsw:hasPart ?epnt .
    ?epnt rdf:type gwsw:EindpuntLeiding .
    ?epnt gwsw:hasConnection ?econn . # Let op: niet optioneel, anders loopt query slecht

      optional {?epnt gwsw:hasAspect
      [ 
          rdf:type gwsw:BobEindpuntLeiding ;
          gwsw:hasValue ?BobEindpuntLeiding ;
      ] .}
      
  # Hou het kort voor MDS (Delft liep vast op het vervolg)

      ?econn gwsw:isAspectOf ?_uriE . # lei in de query-resultaten voor zoekwerk (20200708)
      ?_uriE rdfs:label ?eindpunt .
  }
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:Begindatum ;
      gwsw:hasValue ?Begindatum ;
  ] .}
  optional {?obj gwsw:hasAspect # Deze ontbrak (20230416)
  [
      rdf:type gwsw:Einddatum ;
      gwsw:hasValue ?Einddatum ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:MateriaalLeiding ;
      gwsw:hasReference ?MateriaalLeiding ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:VormLeiding ;
      gwsw:hasReference ?VormLeiding ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:DiameterLeiding ;
# Werkt niet altijd goed? Opgelost via COALESCE (20210521)
#      gwsw:hasValue ?BreedteLeiding ; # Diameter als breedte vermelden
       gwsw:hasValue ?dia ;  
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:BreedteLeiding ;
      gwsw:hasValue ?br ;
  ] .}
  BIND (COALESCE (?br, ?dia) as ?BreedteLeiding) # de eerst geldende doet mee (20210521)

  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:HoogteLeiding ;
      gwsw:hasValue ?HoogteLeiding ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:LengteLeiding ;
      gwsw:hasValue ?LengteLeiding ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:Wanddikte ;
      gwsw:hasValue ?Wanddikte ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:Verbindingstype ;
      gwsw:hasReference ?Verbindingstype ;
  ] .}
  optional {?obj gwsw:hasPart 
  [
      rdf:type gwsw:Fundering ;
      sesame:directType ?Fundering ;
  ] .}
  optional {?obj gwsw:isInputOf 
  [
    rdf:type gwsw:Inspecteren ;
    gwsw:hasAspect
    [
      rdf:type gwsw:DatumMaatregel ;
      gwsw:hasValue ?DatumInspectie ;
    ];
  ] .}
  optional {?obj gwsw:isInputOf 
  [
    rdf:type gwsw:Reinigen ;
    gwsw:hasAspect
    [
      rdf:type gwsw:DatumMaatregel ;
      gwsw:hasValue ?DatumReiniging ;
    ];
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:WIONThema ;
      gwsw:hasReference ?WIONThema ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:AantalWoningen ;
      gwsw:hasValue ?AantalWoningen ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:Aantal_ieBedrijven ;
      gwsw:hasValue ?Aantal_ieBedrijven ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:Aantal_ieRecreatie ;
      gwsw:hasValue ?Aantal_ieRecreatie ;
  ] .}
  optional
  {
      SELECT ?obj (sum(?ao) as ?AfvoerendOppervlak) # sommeer de types ao
      WHERE
      {
          optional {?obj gwsw:hasAspect 
          [
              rdf:type gwsw:AfvoerendOppervlak ;
              gwsw:hasValue ?ao ;
          ] .}
      }
      GROUP BY ?obj
  }
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:LateraalDebietDWA ;
      gwsw:hasValue ?LateraalDebietDWA ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:LateraalDebietHWA ;
      gwsw:hasValue ?LateraalDebietHWA ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:LateraalAfvoerendOppervlak ;
      gwsw:hasValue ?LateraalAfvoerendOppervlak ;
  ] .}
  #BIND (strafter(str(?obj), "#") AS ?uri)
  BIND (?obj AS ?uri) # De complete IRI in de uitvoer (20230416)

  #BIND (IF (BOUND(?_uriB), strafter(str(?_uriB), "#"), "") AS ?uriB)
  #BIND (IF (BOUND(?_uriE), strafter(str(?_uriE), "#"), "") AS ?uriE)
  BIND (IF (BOUND(?_uriB), ?_uriB, "") AS ?uriB) # De complete IRI in de uitvoer (20230416)
  BIND (IF (BOUND(?_uriE), ?_uriE, "") AS ?uriE) # De complete IRI in de uitvoer (20230416)
}
