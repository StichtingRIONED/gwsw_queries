# Default_Punt_deel (20250915 20201016)
# Alle delen van een punt-compositieobject (Doorlaat, Pomp, Wand, Deksel) (ook die met eigen geometrie)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX gwsw: <http://data.gwsw.nl/1.6/totaal/>
PREFIX gwsw: <{{&version}}>

SELECT ?Stelsel ?stelseltype ?naamPunt ?uriPunt ?naam ?uri ?type ?Punt ?begin ?eind ?BreedteOpening ?HoogteOpening ?VormOpening ?Doorlaatniveau ?Contractiecoef ?MaxCapDoorlaat  ?Pompcapaciteit ?AanslagniveauBeneden ?AfslagniveauBeneden ?AanslagniveauBoven ?AfslagniveauBoven ?Drempelbreedte ?Drempelniveau ?Stromingsrichting

WHERE
{ 
  ?obj gwsw:hasAspect ?oriObj . #rdf:type gwsw:TopologischElement ;
  ?oriObj sesame:directType ?oriType .
  FILTER (?oriType IN (gwsw:Bouwwerkorientatie, gwsw:Putorientatie, gwsw:Hulpstukorientatie)) # Alleen de punt-compositieobjecten (sync met Default_Punt_deel)

  ?oriObj gwsw:hasAspect
  [ 
    rdf:type gwsw:Punt ;
    gwsw:hasValue ?objPunt ;
  ] .
  #FILTER (!(isBlank(?obj)))

  # Alleen de delen van een compositie-object

  { ?obj gwsw:hasPart [ gwsw:hasPart ?ond ;] . } # Bijv. via stuwconstructie
  UNION
  { ?obj gwsw:hasPart ?ond . }

  # Dit deel staat al als attribuut in Default_Punt (komt max 1x voor)
  FILTER (NOT EXISTS{?ond rdf:type gwsw:Fundering})

  ?ond sesame:directType ?type .  
  FILTER (!(isBlank(?type)))

  # Consequent alle delen, ook compartimenten met eigen Punt. Op dit niveau komen de put-gegevens ook goed mee.
  #  optional {
  #  ?ond gwsw:hasAspect 
  #  [
  #    rdf:type gwsw:TopologischElement ;
  #    gwsw:hasAspect
  #    [ 
  #      rdf:type gwsw:Punt ;
  #      gwsw:hasValue ?ondPunt ;
  #    ] ;
  #  ] .
  #}
  #FILTER (!BOUND(?ondPunt)) # Staat al in andere GIS-laag

  ?obj rdfs:label ?naamPunt .

  #BIND (strafter(str(?obj), "#") AS ?uriPunt) 
  #BIND (strafter(str(?ond), "#") AS ?uri) 
  BIND (?obj AS ?uriPunt) # De complete IRI in de uitvoer (20230416)
  BIND (?ond AS ?uri) # De complete IRI in de uitvoer (20230416)

  optional { # heeft het deel zelf een punt-geometrie? (20250915)
    ?ond gwsw:hasAspect 
    [
      rdf:type gwsw:TopologischElement ;
      gwsw:hasAspect
      [ 
      rdf:type gwsw:Punt ;
      gwsw:hasValue ?ondPunt ;
      ] ;
    ] .
  }
  BIND (COALESCE(?ondPunt, ?objPunt) AS ?Punt)

  optional
  {
    ?ond rdfs:label ?naam .
  }
 
  optional
  {
    ?obj gwsw:isPartOf 
    [ rdf:type gwsw:Stelsel ;
      rdfs:label ?Stelsel ;
      sesame:directType ?stelseltype ;
    ] .
  }
  
  # de orientatie/verbinding mag er zijn (bij Hyd: moet er zijn)

  optional
  {
    ?ond gwsw:hasAspect ?ori .
	  ?ori rdf:type gwsw:Onderdeelorientatie .
    
    # de verbinding begin/eind

    optional
    {
      ?ori gwsw:hasPart [
          rdf:type gwsw:BeginpuntOnderdeel ;
          gwsw:hasConnection ?bpunt ; ] .
    
      { ?bpunt gwsw:isAspectOf [ rdfs:label ?begin ;] }
      UNION
      { ?bpunt gwsw:isPartOf [ gwsw:isAspectOf [ rdfs:label ?begin ;] ] . }
    }
    optional
    {
      ?ori gwsw:hasPart [
        rdf:type gwsw:EindpuntOnderdeel ;
        gwsw:hasConnection ?epunt ; ] .
      
      { ?epunt gwsw:isAspectOf [ rdfs:label ?eind ;] }
      UNION
      { ?epunt gwsw:isPartOf [ gwsw:isAspectOf [ rdfs:label ?eind ;] ] . }
    }
  }
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:DiameterOpening ;
    gwsw:hasValue ?BreedteOpening ; # Diameter als breedte vermelden
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:BreedteOpening ;
    gwsw:hasValue ?BreedteOpening ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:HoogteOpening ;
    gwsw:hasValue ?HoogteOpening ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:VormOpening ;
    gwsw:hasReference ?VormOpening ;
  ] .}
  optional {?ond gwsw:hasAspect # 20190812
  [
    rdf:type gwsw:Doorlaatniveau ;
    gwsw:hasValue ?Doorlaatniveau ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:ContractiecoefficientDoorlaatprofiel ;
    gwsw:hasValue ?Contractiecoef ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:MaximaleCapaciteitDoorlaat ;
    gwsw:hasValue ?MaxCapDoorlaat ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    sesame:directType gwsw:Pompcapaciteit ; # Niet de subtypes (20201117)
    gwsw:hasValue ?Pompcapaciteit ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:AanslagniveauBenedenstrooms ;
    gwsw:hasValue ?AanslagniveauBeneden ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:AfslagniveauBenedenstrooms ;
    gwsw:hasValue ?AfslagniveauBeneden ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:AanslagniveauBovenstrooms ;
    gwsw:hasValue ?AanslagniveauBoven ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:AfslagniveauBovenstrooms ;
    gwsw:hasValue ?AfslagniveauBoven ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:Drempelbreedte ;
    gwsw:hasValue ?Drempelbreedte ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:Drempelniveau ;
    gwsw:hasValue ?Drempelniveau ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:Stromingsrichting ;
    gwsw:hasReference ?Stromingsrichting ;
  ] .}
}
