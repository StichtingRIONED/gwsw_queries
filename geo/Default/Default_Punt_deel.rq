# Geoserver: Default_Punt_Deel (20201016)
# Alle delen van een punt-object (Doorlaat, Pomp, Wand, Deksel)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX : <http://data.gwsw.nl/1.5/totaal/>
PREFIX : <{{&version}}>

SELECT ?Stelsel ?stelseltype ?naamPunt ?uriPunt ?naam ?uri ?type ?Punt ?begin ?eind ?BreedteOpening ?HoogteOpening ?VormOpening ?Doorlaatniveau ?Contractiecoef ?MaxCapDoorlaat  ?Pompcapaciteit ?AanslagniveauBeneden ?AfslagniveauBeneden ?AanslagniveauBoven ?AfslagniveauBoven ?Drempelbreedte ?Drempelniveau ?Stromingsrichting

WHERE
{ 
  ?put :hasAspect 
  [
    rdf:type :TopologischElement ;
    :hasAspect
    [ 
    rdf:type :Punt ;
    :hasValue ?Punt ;
    ] ;
  ] .
  FILTER (!(isBlank(?put)))

  # Alleen de delen van een Punt (Put, Bouwwerk)

  { ?put :hasPart [ :hasPart ?ond ;] . } # Bijv. via stuwconstructie
  UNION
  { ?put :hasPart ?ond . }

  # Dit deel staat al in Default_Punt (komt max 1x voor)
  FILTER (NOT EXISTS{?ond rdf:type :Fundering})

  ?ond sesame:directType ?type .  
  FILTER (!(isBlank(?type)))

  # Consequent alle delen, ook compartimenten met eigen Punt. Op dit niveau komen de put-gegevens ook goed mee.
  #  optional {
  #  ?ond :hasAspect 
  #  [
  #    rdf:type :TopologischElement ;
  #    :hasAspect
  #    [ 
  #      rdf:type :Punt ;
  #      :hasValue ?ondPunt ;
  #    ] ;
  #  ] .
  #}
  #FILTER (!BOUND(?ondPunt)) # Staat al in andere GIS-laag

  ?put rdfs:label ?naamPunt .

  BIND (strafter(str(?put), "#") AS ?uriPunt) 
  BIND (strafter(str(?ond), "#") AS ?uri) 

  optional
  {
    ?ond rdfs:label ?naam .
  }
 
  optional
  {
    ?put :isPartOf 
    [ rdf:type :Stelsel ;
      rdfs:label ?Stelsel ;
        sesame:directType ?stelseltype ;
    ] .
  }
  
  # de orientatie/verbinding mag er zijn (bij Hyd: moet er zijn)

  optional
  {
    ?ond :hasAspect ?ori .
	  ?ori rdf:type :Onderdeelorientatie .
    
    # de verbinding begin/eind

    optional
    {
      ?ori :hasPart [
          rdf:type :BeginpuntOnderdeel ;
          :hasConnection ?bpunt ; ] .
    
      { ?bpunt :isAspectOf [ rdfs:label ?begin ;] }
      UNION
      { ?bpunt :isPartOf [ :isAspectOf [ rdfs:label ?begin ;] ] . }
    }
    optional
    {
      ?ori :hasPart [
        rdf:type :EindpuntOnderdeel ;
        :hasConnection ?epunt ; ] .
      
      { ?epunt :isAspectOf [ rdfs:label ?eind ;] }
      UNION
      { ?epunt :isPartOf [ :isAspectOf [ rdfs:label ?eind ;] ] . }
    }
  }
  optional {?ond :hasAspect 
  [
    rdf:type :DiameterOpening ;
    :hasValue ?BreedteOpening ; # Diameter als breedte vermelden
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :BreedteOpening ;
    :hasValue ?BreedteOpening ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :HoogteOpening ;
    :hasValue ?HoogteOpening ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :VormOpening ;
    :hasReference ?VormOpening ;
  ] .}
  optional {?ond :hasAspect # 20190812
  [
    rdf:type :Doorlaatniveau ;
    :hasValue ?Doorlaatniveau ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :ContractiecoefficientDoorlaatprofiel ;
    :hasValue ?Contractiecoef ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :MaximaleCapaciteitDoorlaat ;
    :hasValue ?MaxCapDoorlaat ;
  ] .}
  optional {?ond :hasAspect 
  [
    sesame:directType :Pompcapaciteit ; # Niet de subtypes (20201117)
    :hasValue ?Pompcapaciteit ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :AanslagniveauBenedenstrooms ;
    :hasValue ?AanslagniveauBeneden ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :AfslagniveauBenedenstrooms ;
    :hasValue ?AfslagniveauBeneden ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :AanslagniveauBovenstrooms ;
    :hasValue ?AanslagniveauBoven ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :AfslagniveauBovenstrooms ;
    :hasValue ?AfslagniveauBoven ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :Drempelbreedte ;
    :hasValue ?Drempelbreedte ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :Drempelniveau ;
    :hasValue ?Drempelniveau ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :Stromingsrichting ;
    :hasReference ?Stromingsrichting ;
  ] .}
}
