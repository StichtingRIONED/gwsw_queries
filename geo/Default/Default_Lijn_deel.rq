# Geoserver: Default_Lijn_Deel (20220301)
# Alle delen van een lijn-object (Lining, ...)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX : <http://data.gwsw.nl/1.5/totaal/>
PREFIX : <{{&version}}>

SELECT ?Stelsel ?stelseltype ?naamLijn ?uriLijn ?naam ?uri ?type ?Lijn ?Begindatum ?Einddatum ?DikteLining ?MateriaalLining

WHERE
{ 
  ?lei :hasAspect 
  [
    rdf:type :TopologischElement ;
    :hasAspect
    [ 
    rdf:type :Lijn ;
    :hasValue ?Lijn ;
    ] ;
  ] .
  FILTER (!(isBlank(?lei)))

  # Alleen de delen van een Lijn (Leiding, ...)

  { ?lei :hasPart ?ond . }

  # Dit deel staat al in Default_Lijn (komt max 1x voor)
  FILTER (NOT EXISTS{?ond rdf:type :Fundering})

  ?ond sesame:directType ?type .  
  FILTER (!(isBlank(?type)))

  ?lei rdfs:label ?naamLijn .

  # BIND (strafter(str(?lei), "#") AS ?uriPunt) 
  # BIND (strafter(str(?ond), "#") AS ?uri) 
  BIND  (?lei as ?uriLijn) # uri compeeet houden
  BIND  (?ond as ?uri) # uri compeeet houden

  optional
  {
    ?ond rdfs:label ?naam .
  }
  optional
  {
    ?lei :isPartOf 
    [ rdf:type :Stelsel ;
      rdfs:label ?Stelsel ;
        sesame:directType ?stelseltype ;
    ] .
  }
  optional {?ond :hasAspect 
  [
      rdf:type :Begindatum ;
      :hasValue ?Begindatum ;
  ] .}
  optional {?ond :hasAspect 
  [
      rdf:type :Einddatum ;
      :hasValue ?Einddatum ;
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :DikteLining ;
    :hasValue ?DikteLining ; # Diameter als breedte vermelden
  ] .}
  optional {?ond :hasAspect 
  [
    rdf:type :MateriaalLining ;
    :hasValue ?MateriaalLining ; # Diameter als breedte vermelden
  ] .}
}
