# GWSW Geo: Default_Lijn_deel (20220301)
# Alle delen van een lijn-object (Lining, ...)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX gwsw: <http://data.gwsw.nl/1.6/totaal/>
PREFIX gwsw: <{{&version}}>

SELECT ?Stelsel ?stelseltype ?naamLijn ?uriLijn ?naam ?uri ?type ?Lijn ?Begindatum ?Einddatum ?DikteLining ?MateriaalLining

WHERE
{ 
  ?obj gwsw:hasAspect 
  [
    rdf:type gwsw:TopologischElement ;
    gwsw:hasAspect
    [ 
    rdf:type gwsw:Lijn ;
    gwsw:hasValue ?Lijn ;
    ] ;
  ] .
  FILTER (!(isBlank(?obj)))

  # Alleen de delen van een Lijn (Leiding, ...)

  ?obj gwsw:hasPart ?ond .

  # Dit deel staat al in Default_Lijn (komt max 1x voor)
  FILTER (NOT EXISTS{?ond rdf:type gwsw:Fundering})

  ?ond sesame:directType ?type .  
  FILTER (!(isBlank(?type)))

  ?obj rdfs:label ?naamLijn .

  # BIND (strafter(str(?obj), "#") AS ?uriPunt) 
  # BIND (strafter(str(?ond), "#") AS ?uri) 
  BIND  (?obj as ?uriLijn) # uri compteet houden
  BIND  (?ond as ?uri) # uri compeeet houden

  optional
  {
    ?ond rdfs:label ?naam .
  }
  optional
  {
    ?obj gwsw:isPartOf 
    [ rdf:type gwsw:Stelsel ;
      rdfs:label ?Stelsel ;
        sesame:directType ?stelseltype ;
    ] .
  }
  optional {?ond gwsw:hasAspect 
  [
      rdf:type gwsw:Begindatum ;
      gwsw:hasValue ?Begindatum ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
      rdf:type gwsw:Einddatum ;
      gwsw:hasValue ?Einddatum ;
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:DikteLining ;
    gwsw:hasValue ?DikteLining ; # Diameter als breedte vermelden
  ] .}
  optional {?ond gwsw:hasAspect 
  [
    rdf:type gwsw:MateriaalLining ;
    gwsw:hasValue ?MateriaalLining ; # Diameter als breedte vermelden
  ] .}
}
