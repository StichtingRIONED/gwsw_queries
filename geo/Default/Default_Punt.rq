PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX gwsw: <http://data.gwsw.nl/1.5/totaal/>
PREFIX gwsw: <{{&version}}>

SELECT ?Stelsel ?stelseltype ?naam ?uri ?type ?Punt ?Begindatum ?Einddatum ?Maaiveldhoogte ?MateriaalPut ?VormPut ?BreedtePut ?LengtePut ?HoogtePut ?BreedteBouwwerk ?LengteBouwwerk ?BreedteCompartiment ?LengteCompartiment ?Wanddikte ?Fundering ?DatumReiniging ?DatumInspectie ?AantalWoningen ?Aantal_ieBedrijven ?Aantal_ieRecreatie ?AfvoerendOppervlak ?BergendOppervlak ?LateraalDebietDWA ?LateraalDebietHWA ?LateraalAfvoerendOppervlak ?aantalPompen ?aantalDrempels
#SELECT ?Stelsel ?Naam ?Type ?TypeNaam ?Geometrie

WHERE
{ 
  {
    ?obj gwsw:hasAspect ?ori .
    ?ori rdf:type gwsw:TopologischElement .
  }
  UNION # ook de zelfstandige topologische elementen
  {
    ?obj rdf:type gwsw:Afvoerpunt .
    BIND (?obj as ?ori)
  }
  UNION # ook de zelfstandige topologische elementen
  {
    ?obj rdf:type gwsw:Aansluitpunt . # Supertype van Uitlaatpunt en zo (20230426) (hier stond het foute gwsw:Aanvoerpunt)
    BIND (?obj as ?ori)
  }
  FILTER (!(isBlank(?obj)))
  ?obj sesame:directType ?type .
  FILTER (!(isBlank(?type)))
  
  ?ori gwsw:hasAspect
  [ 
    rdf:type gwsw:Punt ;
    gwsw:hasValue ?Punt ;
  ] .
   # ?uri_type rdfs:label ?lab_type . # de conceptnaam
   # FILTER (lang(?lab_type) = "nl") # Niet meer nodig in GWSW 2.0 ? - prefLabel
   # BIND (CONCAT(STR(?lab_type), " ", STR(?uri_type)) AS ?type)
    
   optional { ?obj rdfs:label ?naam . } # niet meer verplicht (alleen geometrie moet kloppen) (20220518)   

   # De uri verwijst naar de GWSW-definitie, daar is de naam enz. te vinden
   #?Type rdfs:label ?TypeNaam . # De URI hoeft niet dekkend te zijn
   #FILTER (lang(?TypeNaam) = "nl") # let op! (niet meer nodig in GWSW 2.0 - prefLabel)
 
   optional
   {
      ?obj gwsw:isPartOf 
      [ rdf:type gwsw:Stelsel ;
        rdfs:label ?Stelsel ;
        sesame:directType ?stelseltype ;
        # sesame:directType ?uri_stelseltype ;
     ] .
    # ?uri_stelseltype rdfs:label ?lab_stelseltype .
    # FILTER (lang(?lab_stelseltype) = "nl") # Niet meer nodig in GWSW 2.0 ? - prefLabel
    # BIND (CONCAT(STR(?lab_stelseltype), " ", STR(?uri_stelseltype)) AS ?stelseltype)
   }
   optional {?ori gwsw:hasConnection
   [
     rdf:type gwsw:Maaiveldorientatie ;
     gwsw:hasAspect
     [
        rdf:type gwsw:Maaiveldhoogte ;
        gwsw:hasValue ?Maaiveldhoogte ;
     ];
   ] .}
   optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:Begindatum ;
       gwsw:hasValue ?Begindatum ;
   ] .}
   optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:Einddatum ;
       gwsw:hasValue ?Einddatum ;
   ] .}
   optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:MateriaalPut ;
       gwsw:hasReference ?MateriaalPut ;
   ] . }
   optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:VormPut ;
       gwsw:hasReference ?VormPut ;
   ] . }
   optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:DiameterPut ;
   # Werkt niet altijd goed? Opgelost via COALESCE (20230710)
   #      gwsw:hasValue ?BreedtePut ; # Diameter als breedte vermelden
       gwsw:hasValue ?dia ;  
   ] .}
   optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:BreedtePut ;
       gwsw:hasValue ?br ;
   ] .}
   BIND (COALESCE (?br, ?dia) as ?BreedtePut) # de eerst geldende doet mee (20230710)

   optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:LengtePut ;
       gwsw:hasValue ?LengtePut ;
   ] .}
      optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:HoogtePut ;
       gwsw:hasValue ?HoogtePut ;
   ] .}
    optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:BreedteBouwwerk ;
       gwsw:hasValue ?BreedteBouwwerk ;
   ] .}
   optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:LengteBouwwerk ;
       gwsw:hasValue ?LengteBouwwerk ;
   ] .}
   optional {?obj gwsw:hasAspect 
   [
     rdf:type gwsw:BreedteCompartiment ;
     gwsw:hasValue ?BreedteCompartiment ;
   ] .}
   optional {?obj gwsw:hasAspect 
   [
     rdf:type gwsw:LengteCompartiment ;
     gwsw:hasValue ?LengteCompartiment ;
   ] .}
   optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:Wanddikte ;
       gwsw:hasValue ?Wanddikte ;
   ] .}
   optional {?obj gwsw:hasPart 
   [
       rdf:type gwsw:Fundering ;
       sesame:directType ?Fundering ;
   ] .}   
   optional {?obj gwsw:isInputOf 
   [
     rdf:type gwsw:Inspecteren ;
     gwsw:hasAspect
     [
        rdf:type gwsw:DatumMaatregel ;
        gwsw:hasValue ?DatumInspectie ;
     ];
   ] .}
  optional {?obj gwsw:isInputOf 
   [
     rdf:type gwsw:Reinigen ;
     gwsw:hasAspect
     [
        rdf:type gwsw:DatumMaatregel ;
        gwsw:hasValue ?DatumReiniging ;
     ];
   ] .}
  optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:AantalWoningen ;
       gwsw:hasValue ?AantalWoningen ;
   ] .}
  optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:Aantal_ieBedrijven ;
       gwsw:hasValue ?Aantal_ieBedrijven ;
   ] .}
  optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:Aantal_ieRecreatie ;
       gwsw:hasValue ?Aantal_ieRecreatie ;
   ] .}
  optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:AfvoerendOppervlak ;
       gwsw:hasValue ?AfvoerendOppervlak ;
   ] .}
  optional {?obj gwsw:hasAspect 
   [
       rdf:type gwsw:BergendOppervlak ;
       gwsw:hasValue ?BergendOppervlak ;
   ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:LateraalDebietDWA ;
      gwsw:hasValue ?LateraalDebietDWA ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:LateraalDebietHWA ;
      gwsw:hasValue ?LateraalDebietHWA ;
  ] .}
  optional {?obj gwsw:hasAspect 
  [
      rdf:type gwsw:LateraalAfvoerendOppervlak ;
      gwsw:hasValue ?LateraalAfvoerendOppervlak ;
  ] .}
  optional {
    SELECT ?obj (count(*) as ?aantalDrempels) # tel het aantal triples
    WHERE
    {
      ?obj ?hasPart ?drp .
      ?drp rdf:type gwsw:Overstortdrempel .
    } GROUP BY ?obj
  }
  optional {
    SELECT ?obj (count(*) as ?aantalPompen) # tel het aantal triples
    WHERE
    {
      ?obj ?hasPart ?pmp .
      ?pmp rdf:type gwsw:Pomp .
    } GROUP BY ?obj
  }
  BIND (?obj AS ?uri) # De complete IRI in de uitvoer (20230416)
  #BIND (strafter(str(?obj), "#") AS ?uri)
}
