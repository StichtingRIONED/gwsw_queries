SELECT ?naam ?geo ?lei_lengte ?gemeente_naam ?wijk_naam ?buurt ?buurt_naam ?buurt_geo ?inwoners_aantal ?huishoudens_aantal ?bedrijven_aantal ?recreatie_aantal
WHERE 
{
  {
	SELECT ?naam ?geo ?rg_wkt (SUM (?lengte) AS ?lei_lengte)
	WHERE
	{
		?rg a gwsw:Rioleringsgebied ;
			rdfs:label ?naam ;
			gwsw:hasAspect ?ori .
		?ori a gwsw:Gebiedsorientatie ;
			 gwsw:hasAspect ?bg .
		?bg a gwsw:Buitengrens ;
			gwsw:hasValue ?geo ;
			geo:asWKT ?rg_wkt .

		?rg gwsw:hasPart ?rs .
		?rs rdfs:label ?rs_naam ;
			gwsw:hasPart ?lei .
		?lei a gwsw:Leiding .

		?lei gwsw:hasAspect [ 
			a gwsw:LengteLeiding ; 
			gwsw:hasValue ?lengte ;
			] .
	}
	GROUP BY ?naam ?geo ?rg_wkt
  }
  SERVICE <https://betalinkeddata.cbs.nl/sparql> 
  {
	  { ?gemeente rdfs:label "Castricum"@nl . }
	  UNION   
	  { ?gemeente rdfs:label "Alkmaar"@nl . } 
	  
	  ?gemeente rdfs:label ?gemeente_naam .

	  ?buurt a cbs:Buurt ;
			 rdfs:label ?buurt_naam ;
			 geo:hasGeometry 
			 [  geo:asWKT ?buurt_wkt ; 
				pdok:asWKT-RD ?buurt_geo ; 
			 ] ;
			?within ?wijk .
	  ?wijk a cbs:Wijk ;
	  		 rdfs:label ?wijk_naam ;
	  		 geo:hasGeometry 
	  	 	 [  geo:asWKT ?wijk_wkt ; 
	  			#pdok:asWKT-RD ?wijk_wkt_rd ; 
	  		 ] ;
	  		?within ?gemeente .

	  filter (geof:sfIntersects(?buurt_wkt, ?rg_wkt))   

	  # Veelzeggende eigenschappen van de buurt
	  ?inwoners cbs_dim:regio ?buurt ;
				cbs_qb:inObservationGroup cbs_slice:bevolking_AantalInwoners ;
				cbs_83487:bevolking_AantalInwoners ?inwoners_aantal .

	  ?huishoudens cbs_dim:regio ?buurt ;
				   cbs_qb:inObservationGroup cbs_slice:bevolking_ParticuliereHuishoudens_HuishoudensTotaal ;
				   cbs_83487:bevolking_ParticuliereHuishoudens_HuishoudensTotaal ?huishoudens_aantal .
	  
	  optional
	  {
		?bedrijven cbs_dim:regio ?buurt ;
				   cbs_qb:inObservationGroup cbs_slice:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenTotaal ;
				   cbs_83487:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenTotaal ?bedrijven_aantal .
	  }
	  optional
	  {
		?recreatie cbs_dim:regio ?buurt ;
				   cbs_qb:inObservationGroup cbs_slice:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenNaarActiviteit_R-uCultuur_Recreatie_OverigeDiensten ;
				   cbs_83487:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenNaarActiviteit_R-uCultuur_Recreatie_OverigeDiensten ?recreatie_aantal .
	  }
  }
}
ORDER BY ?naam ?buurt_naam
