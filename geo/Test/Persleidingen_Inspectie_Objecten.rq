# Inspecteren persleidingen - De objecten: buisdeel, persleiding (20241120)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX gwsw: <http://data.gwsw.nl/1.6/totaal/>
PREFIX gwsw: <{{&version}}>

SELECT ?lei ?naamPersleiding ?naam ?type ?Opmerking ?DatumVanInwinning ?WijzeVanInwinning ?materiaal ?breedte ?hoogte ?Lijn

WHERE
{ 
  ?actv rdf:type gwsw:InspecterenPersleiding . # Moet eigenlijk op persleiding/buisdeel niveau? 
  ?actv gwsw:hasOutput ?lei .
  { 
    #?actv rdf:type gwsw:InspecterenPersleiding .
    #?actv gwsw:hasOutput ?lei .

    optional {?lei gwsw:hasAspect [
      rdf:type gwsw:MateriaalLeiding ;
      gwsw:hasReference ?materiaal ;
    ] . }
    optional {?lei gwsw:hasAspect [
      rdf:type gwsw:BreedteLeiding ;
      gwsw:hasValue ?breedte ;
    ] . }
    optional {?lei gwsw:hasAspect [ # Breedte als diameter vermelden (persleiding is altijd rond)
      rdf:type gwsw:HoogteLeiding ;
      gwsw:hasValue ?hoogte ;
    ] . }
    ?lei gwsw:hasAspect [
      rdf:type gwsw:Leidingorientatie ;
      gwsw:hasAspect [
        rdf:type gwsw:Lijn ;
        gwsw:hasValue ?Lijn ;
      ] ;
    ] .
  }
  UNION { 
    #?actv rdf:type gwsw:InspecterenBuisdeel .
    #?actv gwsw:hasOutput ?lei .

    optional {?lei gwsw:hasAspect [
      rdf:type gwsw:MateriaalBuisdeel ;
      gwsw:hasReference ?materiaal ;
    ] . }
    optional {?lei gwsw:hasAspect [
      rdf:type gwsw:BreedteBuisdeel ;
      gwsw:hasValue ?breedte ;
    ] . }
    optional {?lei gwsw:hasAspect [ # Breedte als diameter vermelden (buisdeel is altijd rond)
      rdf:type gwsw:HoogteBuisdeel ;
      gwsw:hasValue ?hoogte ;
    ] . }
    ?lei gwsw:hasAspect [
      rdf:type gwsw:Constructieonderdeelorientatie ;
      gwsw:hasAspect [
        rdf:type gwsw:Lijn ;
        gwsw:hasValue ?Lijn ;
      ] ;
    ] .
  }
  ?lei sesame:directType ?type .
  ?lei rdfs:label ?naam .
  
  optional {?lei gwsw:isPartOf [
      rdfs:label ?naamPersleiding ;
    ] .
  }
  optional {
    ?actv gwsw:hasAspect [
      rdf:type gwsw:DatumVanInwinning ;
      gwsw:hasValue ?DatumVanInwinning ;
    ] .
  }
  optional {
    ?actv gwsw:hasAspect [
      rdf:type gwsw:WijzeVanInwinning ;
      gwsw:hasReference ?WijzeVanInwinning ;
    ] .
  }
  optional {
    ?actv gwsw:hasAspect [
      rdf:type gwsw:Opmerking ;
      gwsw:hasValue ?Opmerking ;
    ] .
  }

}
