# GWSW_Lijsten Relaties (20190429)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX : <http://data.gwsw.nl/1.5/totaal/>

SELECT DISTINCT ?OwnerOfProperty ?Subject ?Property ?Object

FROM <http://www.ontotext.com/explicit> # GraphDB: gebruik alleen expliciete (not-inferred) binnen default Graph

where
{
# ?b = blank node
 ?Class1 rdfs:subClassOf ?b .

 # zoek de restricties op cardinaliteit

 ?b owl:onProperty ?property .
 ?b owl:onClass ?object .

 ?Class2 rdfs:subClassOf* ?Class1 . # Property path: 0 of meer links
 ?Class2 :hasDateStart ?dat . # alleen GWSW-classes gebruiken

# BIND ("#" AS ?aft)
# BIND ("gwsw.nl/" AS ?aft)
  BIND ("1.5/totaal/" AS ?aft)

 BIND (strafter(str(?Class1), ?aft) AS ?OwnerOfProperty)
 BIND (strafter(str(?Class2), ?aft) AS ?Subject)
 BIND (strafter(str(?object), ?aft) AS ?Object)
 BIND (strafter(str(?property), ?aft) AS ?Property)
}
