# Test op type = GWSW-class

# aangepast op separate CFK in primaire dataset (20190814)
# In de dataset zit nu standaard de complete ontologie, CFK niet in gebruik

where
{
    SERVICE <{{&endpoint}}> # endpoint wordt de te valideren dataset 
	{
		?s sesame:directType ?t .
		
		BIND ("{{&version}}" AS ?aft)
		
		filter (isBLANK(?s) || !contains(str(?s), ?aft)) # Instance is geen GWSW-URI (string compare werkt niet op blanks?, aangevuld) (20190410)
		filter (contains(str(?t), ?aft)) # Type is wel GWSW-URI
		filter (NOT EXISTS{?t rdf:type owl:Class}) # Geen class
			
		BIND (strafter(str(?t), ?aft) as ?sTyp)
		BIND (?sTyp AS ?sAsp)

		OPTIONAL # label hoeft er niet te zijn bij fout type
		{
			?s rdfs:label ?sLab .
		}
		BIND ("Type onbekend in de gekoppelde GWSW ontologie [type = fout]" as ?typ) # incl. marker voor rapport SomMldTyp (sync a) (20190831)
		BIND ("Vervolg-validaties zijn hierdoor niet betrouwbaar" AS ?msg)
		
		OPTIONAL # Put-geometrie
		{
		   ?s :hasAspect
		   [
			  rdf:type :Putorientatie ;
			  :hasAspect
			  [ 
				rdf:type :Punt ;
				:hasValue ?punt ;
			  ]
		   ]
		}
		OPTIONAL
		{
			?s :hasAspect ?ori .
			?ori rdf:type :Leidingorientatie .
		
			optional # Leiding heeft eigen geo
			{
				?ori :hasAspect
				[ 
					rdf:type :Lijn ;
					:hasValue ?lijn ;
				]
			}
		}
	}
}
