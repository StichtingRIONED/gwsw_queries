# Test op type = GWSW-class

select ?typ ?sTyp ?sLab ?sAsp ?msg ?punt ?lijn

where
{
    ?s sesame:directType ?t .
	
    BIND ("{{&version}}" AS ?aft) # version: 1.4/totaal/
	
    filter (isBLANK(?s) || !contains(str(?s), ?aft)) # Instance is geen GWSW-URI (string compare werkt niet op blanks?, aangevuld) (20190410)
	filter (contains(str(?t), ?aft)) # Type is wel GWSW-URI
    filter (NOT EXISTS{?t rdf:type owl:Class}) # Geen class
        
    BIND (strafter(str(?t), ?aft) as ?sTyp)
    BIND (?sTyp AS ?sAsp)

    OPTIONAL # label hoeft er niet te zijn bij fout type
    {
    	?s rdfs:label ?sLab .
    }
	BIND ("Type onbekend in het gekoppelde GWSW filter" as ?typ)
    BIND ("Vervolg-validaties zijn hierdoor niet betrouwbaar" AS ?msg)
	
	OPTIONAL # Put-geometrie
	{
	   ?s :hasAspect
	   [
		  rdf:type :Putorientatie ;
		  :hasAspect
		  [ 
			rdf:type :Punt ;
			:hasValue ?punt ;
		  ]
	   ]
	}
	OPTIONAL
	{
	    ?s :hasAspect ?ori .
	    ?ori rdf:type :Leidingorientatie .
    
        optional # Leiding heeft eigen geo
        {
            ?ori :hasAspect
            [ 
                rdf:type :Lijn ;
                :hasValue ?geo1 ;
            ]
        }
        optional # Geen eigen geo, zoek via bPunt, ePunt
        {
            ?ori :hasPart ?bpnt .
            ?bpnt rdf:type :BeginpuntLeiding .
            ?ori :hasPart ?epnt .
            ?epnt rdf:type :EindpuntLeiding .

            ?bpnt :hasConnection
            [ 
              :hasAspect
              [ 
                rdf:type :Punt ;
                :hasValue ?bgeo ;
              ]
            ] .
            bind (strafter(strbefore(str(?bgeo), "</gml:pos>"), "<gml:pos>") as ?bxy)
            ?epnt :hasConnection
            [ 
              :hasAspect
              [ 
                rdf:type :Punt ;
                :hasValue ?egeo ;
              ]
            ] .
            bind (strafter(strbefore(str(?egeo), "</gml:pos>"), "<gml:pos>") as ?exy) # lees inhoud gml:pos

            bind (concat("<gml:LineString xmlns:gml=\"http://www.opengis.net/gml\"><gml:posList srsDimension=\"2\">", str(?bxy), " ", str(?exy), "</gml:posList></gml:LineString>") as ?geo2)
        }
        BIND (COALESCE (?geo1, ?geo2) as ?lijn)
	}
}
