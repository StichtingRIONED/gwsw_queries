# 20170720
# Validatie: max 1 functionalproperty
# inferencing op GWSW Ontologie nodig

select ?typ ?sTyp ?sLab ?sAsp ?msg ?punt ?lijn ?bimSub ?prop (COUNT (*) AS ?at)

where 
{ 
    ?p rdf:type owl:FunctionalProperty .
    ?bimSub ?p ?o .
	
    BIND ("{{&version}}" AS ?aft)

    # initialiseer ?sTyp
    
    ?bimSub sesame:directType ?styp .
    BIND (IF(CONTAINS(str(?styp), ?aft), strafter(str(?styp), ?aft), str(?styp)) as ?sTyp)

    # initialiseer ?sLab
   
    OPTIONAL 
    {
        ?bimSub rdfs:label ?s0 .
	}
    OPTIONAL # Aspect van aspect? Zoek naam
    {
        ?bimSub :isAspectOf ?sup1 .
        ?sup1 rdfs:label ?s1 .
    }
    OPTIONAL # Deel van aspect? Zoek naam
    {
        ?bimSub :isPartOf ?_sup .
        ?_sup :isAspectOf ?sup2 .
        ?sup2 rdfs:label ?s2 .
    }
    BIND (COALESCE (?s0, ?s1, ?s2, "") as ?sLab) # Altijd initialiseren voor rapportage

    # initialiseer ?sAsp

    BIND (?sTyp as ?sAsp)
     
    # initialiseer ?msg
    
    BIND ( IF(CONTAINS(str(?p), ?aft), strafter(str(?p), ?aft), str(?p)) as ?prop)
    BIND (CONCAT("Aantal relaties [", str(?prop), "] te groot, maximum = 1") as ?msg)

    # initialiseer ?typ

    BIND (CONCAT("Te veel relaties [", str(?prop), "]")  as ?typ)
}
GROUP BY ?typ ?sTyp ?sLab ?sAsp ?msg ?punt ?lijn ?bimSub ?prop
HAVING (?at > 1)
