# Validatie: test de max-cardinaliteit (ook bovengrens exacte cardinaliteit)
# 20151118-20170310

where
{
	select ?typ ?sTyp ?sLab ?sAsp ?msg ?punt ?lijn ?bimSub ?prop ?At (COUNT (*) AS ?at)

	where 
	{ 
		# variabele: relatie-type
		
	
		# Union voor binding ?p werk niet in combi met SERVICE CFK, via GWSW Apps regelen (20190814)
		# Apart aanroepen voor predicates hasAspect, hasPart, hasInput, hasOutput, hasConnection

		BIND (:{{&predicate}} AS ?p)

		# Union voor binding ?card werk niet in combi met SERVICE CFK, via GWSW Apps regelen (20190814)
		# Apart aanroepen voor predicates qualifiedCardinality, maxQualifiedCardinality

		BIND (owl:{{&predCard}} as ?card)
	   
		?styp rdfs:subClassOf ?cls .
		FILTER (!(isBlank(?styp)))
		
		?cls ?card ?At .
		?cls owl:onProperty ?p .
		?cls owl:onClass ?o .
	 
		SERVICE <{{&endpoint}}> # endpoint wordt de te valideren dataset 
		{
			BIND ("{{&version}}" AS ?aft)
			
			?bimSub sesame:directType ?styp . # alleen het eerste type
			?bimSub ?p ?bimObj .
			?bimObj rdf:type ?o .

			# initialiseer ?sLab
		   
			OPTIONAL
			{
				?bimSub rdfs:label ?s0 .
			}
			OPTIONAL # Aspect van aspect? Zoek naam
			{
				?bimSub :isAspectOf ?sup1 .
				?sup1 rdfs:label ?s1 .
			}
			OPTIONAL # Deel van aspect? Zoek naam
			{
				?bimSub :isPartOf ?_sup .
				?_sup :isAspectOf ?sup2 .
				?sup2 rdfs:label ?s2 .
			}
			BIND (COALESCE (?s0, ?s1, ?s2, "") as ?sLab) # Altijd initialiseren voor rapportage
			BIND (if (bound(?s0), ?bimSub, if(bound(?s1), ?sup1, if(bound(?s2), ?sup2, ""))) as ?s) # De bijbehorende put/lei

			# initialiseer ?sTyp

			BIND ( IF(CONTAINS(str(?styp), ?aft), strafter(str(?styp), ?aft), str(?styp)) as ?sTyp)
			
			# initialiseer ?sAsp

			OPTIONAL
			{
				?o rdfs:label ?asp .
				FILTER(lang(?asp) = "nl") # max 1 regel
			}
			BIND (IF(BOUND(?asp), ?asp, IF(CONTAINS(str(?o), ?aft), strafter(str(?o), ?aft), str(?o))) as ?sAsp)
			#BIND (IF(CONTAINS(str(?o), ?aft), strafter(str(?o), ?aft), str(?o))  as ?sAsp)

			# initialiseer ?msg
		   
			BIND ( IF(CONTAINS(str(?p), ?aft), strafter(str(?p), ?aft), str(?p)) as ?prop)
			BIND (CONCAT("Aantal relaties [", str(?prop), "] te groot, maximum = ", str(?At)) as ?msg)

			# initialiseer ?typ

			BIND (CONCAT("Te veel relaties [", str(?prop), "]")  as ?typ)

			# Uitgeschakeld: gaf vastlopers/vertraging
			#OPTIONAL # Put-geometrie
			#OPTIONAL # Leiding-geometrie 
		} # einde service
	}
	GROUP BY ?typ ?sTyp ?sLab ?sAsp ?msg  ?punt ?lijn ?bimSub ?prop ?At
	HAVING (?at > ?At)
}
