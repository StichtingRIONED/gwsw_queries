# Tbv RIONED Monitor 2028 (20250809) (20240208)

# ?Stelsel ?type bevatten de URI-naam (om wille van snelheid)
# Om het consequent te houden: ook voor ?groep de URI-naam gebruiken

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
#PREFIX gwsw: <http://data.gwsw.nl/1.6/totaal/>
PREFIX gwsw: <{{&version}}>

SELECT ?dataset ?Eigenaar ?Stelsel ?groep ?type (COUNT (?type) AS ?aantal) ?jaar ?Lining (SUM (?LengteLeiding) AS ?somLengte) ?somBergendVermogen ?somInfiltrerendOppervlak
WHERE
{ 
  { ?obj rdf:type gwsw:MechanischeRioolleiding .
    BIND ("MechanischeRioolleiding" as ?groep) }
  UNION {
    ?obj rdf:type gwsw:VrijvervalRioolleiding .
    BIND ("VrijvervalRioolleiding" as ?groep) }
  UNION {
    ?obj rdf:type gwsw:MechanischeTransportleiding .
    BIND ("MechanischeTransportleiding" as ?groep) }
  UNION {
    ?obj rdf:type gwsw:VrijvervalTransportleiding .
    BIND ("VrijvervalTransportleiding" as ?groep) }
  UNION {
    ?obj rdf:type gwsw:OpenLeiding .
    BIND ("OpenLeiding" as ?groep) }
  UNION {
    ?obj rdf:type gwsw:Drain .
    BIND ("Drain" as ?groep) }

  #FILTER NOT EXISTS {?obj rdf:type gwsw:Aansluitleiding .}

  ?obj sesame:directType ?type1 .
  FILTER (!(isBlank(?type1)))
  FILTER NOT EXISTS { ?type1 rdfs:subClassOf gwsw:ExtraObjecttype } # Alleen functionele types (20250812)

  BIND (str(iri(gwsw:)) AS ?aft)
  BIND ( strafter(str(?type1), ?aft) as ?type)

  BIND ("{{&repository}}" AS ?dataset )
  
  # Doe geen nl-labels, vertraagt enorm [langzaam] (20250809)
  #BIND ( strafter(str(?type1), ?aft) as ?mType)
  #OPTIONAL { 
  #  ?type1 rdfs:label ?nType . 
  #  FILTER(lang(?nType) = "nl")
  #}
  #BIND (COALESCE(?nType, ?mType, "") AS ?type)

  OPTIONAL {
    ?obj gwsw:isPartOf [
      rdf:type gwsw:Stelsel ;
   	  sesame:directType ?stelsel1 ;
    ]
 	  BIND ( strafter(str(?stelsel1), ?aft) as ?Stelsel)
    
    # Doe geen nl-labels, vertraagt enorm [langzaam] (20250809)
 	  #BIND ( strafter(str(?stelsel1), ?aft) as ?mStelsel)
    #OPTIONAL { 
    #  ?stelsel1 rdfs:label ?nStelsel . 
    #  FILTER(lang(?nStelsel) = "nl")
    #}
  }
  #BIND (COALESCE(?nStelsel, ?mStelsel, "") as ?Stelsel)

  OPTIONAL {
    ?obj gwsw:hasAspect [
      rdf:type gwsw:Begindatum ;
      gwsw:hasValue ?datum ;
    ] .
 	  BIND (SUBSTR(str(?datum), 1, 4) as ?jaar1)
  }
  BIND (COALESCE(?jaar1, "") as ?jaar)

  # De query loopt niet met ?Eigenaar als die bovenaan staat! (20240208)
  OPTIONAL {
    ?obj gwsw:isInputOf ?eig1 .
    ?eig1 rdf:type gwsw:Eigenaar .
 	  BIND ( strafter(str(?eig1), ?aft) as ?eig2)
  }
  BIND (COALESCE(?eig2, "") as ?Eigenaar)

  OPTIONAL {
    ?obj gwsw:hasAspect [
      rdf:type gwsw:LengteLeiding ;
      gwsw:hasValue ?LengteLeiding ;
    ] .
  }
  OPTIONAL {?obj gwsw:hasPart 
    [
      rdf:type gwsw:Lining ;
    ] .
    BIND ("ja" as ?lining1) 
  }
  BIND (COALESCE(?lining1, "") as ?Lining)

  BIND ("" as ?somBergendVermogen)
  BIND ("" as ?somInfiltrerendOppervlak)
}
GROUP BY ?dataset ?Eigenaar ?Stelsel ?groep ?type ?jaar ?Lining ?somBergendVermogen ?somInfiltrerendOppervlak
