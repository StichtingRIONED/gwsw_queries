# SwChat - Collecties (20251027)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX gwsw: <http://data.gwsw.nl/1.6/totaal/>

select ?rec

FROM <http://www.ontotext.com/explicit> # GraphDB: gebruik alleen expliciete (not-inferred) binnen default Graph

where
{
  BIND (str(iri(gwsw:)) AS ?aft) # De datamodel-prefix
  ?coll rdfs:subClassOf gwsw:VerzamelingSoorten .

  ?cls rdfs:subClassOf [
    owl:allValuesFrom ?coll ;
  ] .
  FILTER (CONTAINS(str(?cls), ?aft))
  ?cls rdfs:label ?lab .
  FILTER (lang(?lab) = "nl") # de nl-versie is er altijd (20251101)
  #BIND (IF (CONTAINS(str(?lab), " "), CONCAT("{{", ?lab, "}}"), ?lab) AS ?labCls) # De klasse met reference naar individu
  BIND (CONCAT("{{", ?lab, "}}") AS ?labCls) # De klasse met reference naar individu

  ?elem rdf:type ?coll .
  ?elem rdfs:label ?labObj .
  FILTER (lang(?labObj) = "nl") # de nl-versie is er altijd (20251101)

  BIND (concat("Een ", ?labCls, " heeft mogelijk als waarde: \"", ?labObj, "\".") as ?rec)
}