# Datamodel: Onderscheidende kenmerken

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

PREFIX gwsw: <http://data.gwsw.nl/1.6/totaal/>
#PREFIX gwsw: <{{&version}}>

SELECT ?rec #?json

# Dit niet doen, relaties worden geÃ«rfd (de lijst wordt 4x zo lang) (20251110)
#FROM <http://www.ontotext.com/explicit> # GrpahDB: gebruik alleen expliciete (not-inferred) binnen default Graph

where
{
  BIND (str(iri(gwsw:)) AS ?aft) # De datamodel-prefix

  ?cls rdf:type owl:Class .
  FILTER (CONTAINS(str(?cls), ?aft))
  ?cls rdfs:label ?lab .
  FILTER (lang(?lab) = "nl") # de nl-versie is er altijd (20251101)
  #BIND (IF (CONTAINS(str(?lab), " "), CONCAT("{{", ?lab, "}}"), ?lab) AS ?labCls)
  BIND (CONCAT("{{", ?lab, "}}") AS ?labCls)

  { # datatype-properties (ok'en)
    { 
      ?cls rdfs:subClassOf [
        owl:onProperty gwsw:functie ;
        owl:hasValue ?obj ;
      ] .
      BIND ("heeft als functie:" as ?pred) 
      BIND ("functie" as ?predJ)
    }
    UNION {
      ?cls rdfs:subClassOf [
        owl:onProperty gwsw:uitvoering ;
        owl:hasValue ?obj ;
        #BIND ("is uitgevoerd als" as ?pred) 
      ] .
      BIND ("heeft als uitvoering:" as ?pred) # Geen voltooid deelwoord (20251104)
      BIND ("uitvoering" as ?predJ)
    }
    UNION {
      ?cls rdfs:subClassOf [
        owl:onProperty gwsw:doel ; # (20251129)
        owl:hasValue ?obj ;
      ] .
      BIND ("heeft als doel:" as ?pred)
    }
    UNION {
      ?cls rdfs:subClassOf [
        owl:onProperty gwsw:toepassing ; # (20251129)
        owl:hasValue ?obj ;
      ] .
      BIND ("heeft als toepassing:" as ?pred) # "wordt toegepast als/voor/bij" (20251104)
    }
    UNION {
      ?cls rdfs:subClassOf [
        owl:onProperty gwsw:structuur ; # (20251130)
        owl:hasValue ?obj ;
      ] .
      BIND ("is opgebouwd als:" as ?pred) 
    }
    UNION {
      ?cls rdfs:subClassOf [
        owl:onProperty gwsw:technologie ; # (20251129)
        owl:hasValue ?obj ;
      ] .
      BIND ("gebruikt als techniek:" as ?pred) 
    }
    UNION {
      ?cls rdfs:subClassOf [
        owl:onProperty gwsw:mechanisme ; # (20251130)
        owl:hasValue ?obj ;
      ] .
      BIND ("wordt uitgevoerd met:" as ?pred) 
    }
    ?obj rdfs:label ?labO .
    BIND (CONCAT("\"", ?labO, "\"") AS ?labObj)
  }
  UNION { # Relaties zonder kardinaliteit, bij eigenschappen is kardinatliteit een soort kwaliteitsmeting, geen model-definitie. (20251122)
    ?cls rdfs:subClassOf ?sub .
    {
      ?sub  owl:onProperty gwsw:hasAspect ;
            owl:onClass ?obj .
      BIND ("heeft als eigenschap:" AS ?pred) 
    }
    UNION {
      ?sub  owl:onProperty gwsw:hasRepresentation ;
            owl:onClass ?obj .
      BIND ("heeft als representatie:" AS ?pred) 
    }
    ?obj rdfs:label ?labO .
    FILTER (lang(?labO) = "nl") # de nl-versie is er altijd (20251101)
    BIND (CONCAT("{{", ?labO, "}}") AS ?labObj)
  }
  UNION { # Relaties met (optionele) kardinaliteit
    ?cls rdfs:subClassOf ?sub .
    {
      ?sub  owl:onProperty gwsw:hasPart ;
            owl:onClass ?obj .
      BIND ("heeft als onderdeel" as ?prd)
    }
    UNION {
      ?sub  owl:onProperty gwsw:hasInput ; # (20251130)
            owl:onClass ?obj .
      BIND ("heeft als inbreng" as ?prd)
    }
    UNION {
      ?sub  owl:onProperty gwsw:hasOutput ; # (20251130)
            owl:onClass ?obj .
      BIND ("heeft als resultaat" as ?prd)
    }
    UNION {
      ?sub  owl:onProperty gwsw:hasConnection ; # (20251130)
            owl:onClass ?obj .
      BIND ("is verbonden met" as ?prd)
    }
    ?obj rdfs:label ?labO .
    FILTER (lang(?labO) = "nl") # de nl-versie is er altijd (20251101)
    BIND (CONCAT("{{", ?labO, "}}") AS ?labObj)

    OPTIONAL { # kardinaliteit erbij (20251031)
      {
        ?sub owl:qualifiedCardinality ?at .
        BIND (CONCAT(" exact ", str(?at)) as ?opm)
      } 
      UNION {
        ?sub owl:maxQualifiedCardinality ?at .
        BIND (CONCAT(" maximaal ", str(?at)) as ?opm)
      }
      UNION {
        ?sub owl:minQualifiedCardinality ?at .
        BIND (IF (str(?at) = "0", "", CONCAT(" minimaal ", str(?at))) as ?opm)
      }
    }
    #BIND ("heeft als onderdeel type" as ?pred) 
    BIND ( IF (STRLEN(?opm), concat(?prd, ?opm), concat(?prd, ":")) as ?pred) # BOUND(?opm) werkt niet... (20251130)
    BIND ("onderdeel" as ?predJ)
  }
  BIND (concat("Een ", ?labCls, " ", ?pred, " ", ?labObj, ".") as ?rec)
  #BIND (concat("{\"id\": \"", ?labCls, "\", \"type\": \"klasse\", \"", ?predJ, "\": \"", ?labObj, "\"}" ) as ?json)
}
