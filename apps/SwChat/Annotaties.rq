# Datamodel: Enkele Annotaties van fysieke objecten (20251013)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

PREFIX gwsw: <http://data.gwsw.nl/1.6/totaal/>
#PREFIX gwsw: <{{&version}}>

SELECT DISTINCT ?rec #?json # Distinc vanwege meervoudige typering collectie-individuen

FROM <http://www.ontotext.com/explicit> # GrpahDB: gebruik alleen expliciete (not-inferred) binnen default Graph

where
{
  BIND (str(iri(gwsw:)) AS ?aft) # De datamodel-prefix

  {
    ?cls rdf:type owl:Class .
    FILTER (CONTAINS(str(?cls), ?aft))
    ?cls rdfs:label ?lab .
    FILTER (lang(?lab) = "nl") # de nl-versie is er altijd (20251101)
    #BIND (IF (CONTAINS(str(?lab), " "), CONCAT("Een {{", ?lab, "}}"), CONCAT("Een ", ?lab)) AS ?labCls) # Hier de "Een" voor het sorteerwerk
    BIND (CONCAT("Een {{", ?lab, "}}") AS ?labCls) # Hier de "Een" voor het sorteerwerk
  } 
  UNION { # Ook annotaties van de individuen binnen collecties (20251030)
    ?coll rdfs:subClassOf gwsw:VerzamelingSoorten .
    ?cls rdf:type ?coll . # ?cls is hier het waarde-individu 
    ?cls rdfs:label ?lab .
    FILTER (lang(?lab) = "nl") # de nl-versie is er altijd (20251101)
    BIND (CONCAT("De waarde \"", ?lab, "\"") AS ?labCls) # De waarde "..."
  }
  {
    ?cls skos:altLabel ?ann . # Ook andere talen
    #BIND ("heeft als synoniem:" as ?pred)
    BIND ("wordt ook wel genoemd:" as ?pred) # Normale mensen-taal (20251128)
    BIND ("synoniem" as ?predJ)
  }
  UNION {
    ?cls skos:definition ?ann .
    BIND ("heeft als definitie:" as ?pred) 
    BIND ("definitie" as ?predJ)
  }
  UNION {
    ?cls rdfs:seeAlso ?ann .
    BIND (IF (CONTAINS(str(?ann), "file:///img"), "heeft als afbeelding:", "heeft als beschrijving:") as ?pred) # was: is beschreven als: ))
    BIND ("beschreven" as ?predJ)
  }
  UNION {
    ?cls gwsw:hasUnit ?ann . # (20251104)
    BIND ("heeft als waarde-eenheid:" as ?pred) 
    BIND ("waarde-eenheid" as ?predJ)
  }
  UNION {
    ?cls skos:notation ?cod . # (20251130)
    BIND (str(?cod) as ?ann) # omzetten naar string, code kan datatype hebben
    BIND ("heeft als code:" as ?pred)
    BIND (strafter(str(datatype(?cod)), "Dt_Notation_") AS ?dattyp) .
    BIND (IF (?dattyp != "", CONCAT(?co, " (", ?dattyp, ")"), ?co) as ?code) .
  }
  UNION { # min/max (20251104)
    ?cls rdfs:subClassOf [
      owl:allValuesFrom ?dt ;
    ] .
    ?dt owl:equivalentClass [
      rdf:type rdfs:Datatype ;
      owl:withRestrictions (
        [ xsd:minInclusive ?min ; ]
        [ xsd:maxInclusive ?max ; ]
      ) ;
    ] .
    OPTIONAL { ?cls gwsw:hasUnit ?u . }
    BIND (IF (BOUND(?u), CONCAT(" ", ?u), "") as ?unit)

    BIND ("heeft als waarde-bereik:" as ?pred)
    BIND (CONCAT("Tussen ", str(?min), " en ", str(?max), str(?unit)) as ?ann)
  }
  UNION {
    BIND (str(?cls) as ?ann) # De GWSW-verwijzing erbij
    BIND ("heeft als beschrijving:" as ?pred)
  }
  #BIND (concat("Type \"", ?labCls, "\" ", ?pred, " \"", ?ann, "\".") as ?rec)
  #BIND (concat("Type ", ?labCls, " ", ?pred, " ", ?ann, ".") as ?rec)
  BIND (concat(?labCls, " ", ?pred, " \"", ?ann, "\".") as ?rec)
  #BIND (concat("{\"id\": \"", ?labCls, "\", \"type\": \"klasse\", \"", ?predJ, "\": \"", ?ann, "\"}" ) as ?json)
}
