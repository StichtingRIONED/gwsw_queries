# Test op juiste typering BIM-objecten
# Kwaliteitsniveau als opmerking bij type opgenomen
# Alleen binnen conformiteitsklassen in gebruik
# 20170307

select ?typ ?sTyp ?sLab ?sAsp ?msg ?punt ?lijn

where
{
	# Gewijzigd, niet meer via OK, nu alleen marker "fout" in commentaar
	## zoek de restricie bij sTyp
 	#?b4 sesame:directSubClassOf ?t .
	#?b4 owl:someValuesFrom ?b5 .
	#?b5 owl:intersectionOf ?b6 .
	#?b6 rdf:first :KwaliteitType . # (first vanuit intersectie van hasAspect (OK) en hasValue (Wrd)
	#?b6 rdf:rest ?b2 .
	#?b2 rdf:first ?b3 .
	#?b3 owl:hasValue ?wrd . # (Wrd = owl:Thing = waarde onderscheidend kenmerk)
    #filter (?wrd = :KwaliteitTyperingOnvoldoende || ?wrd = :KwaliteitTyperingAcceptabel)
    #bind (if (?wrd = :KwaliteitTyperingOnvoldoende, "Kwaliteit typering = fout", "Kwaliteit typering = neutraal") as ?typ)
    
	?t rdfs:comment ?c .
	filter (contains(?c, "fout")) # marker, binnen de geldende cfk is dit een fout type
    ?s sesame:directType ?t . # zijn er instanties van het foute type?
    
    BIND ("gwsw.nl/1.3.1/totaal/" AS ?aft)
    BIND ( IF(contains(str(?t), ?aft), strafter(str(?t), ?aft), str(?t)) as ?sTyp)
    BIND (?sTyp AS ?sAsp)

    OPTIONAL # label moet er eigenlijk wel zijn
    {
    	?s rdfs:label ?lab .
    }
	BIND (IF (BOUND(?lab), ?lab, "Onbekend") as ?sLab)
	BIND ("Kwaliteit typering = onvoldoende (niet conform CFK)" as ?typ)
    BIND ("Vervolg-validaties zijn hierdoor niet betrouwbaar" AS ?msg)
	
	OPTIONAL # Put-geometrie
	{
	   ?s :hasAspect
	   [
		  rdf:type :Putorientatie ;
		  :hasAspect
		  [ 
			rdf:type :Punt ;
			:hasValue ?punt ;
		  ]
	   ]
	}
	OPTIONAL
	{
	    ?s :hasAspect ?ori .
	    ?ori rdf:type :Leidingorientatie .
    
        optional # Leiding heeft eigen geo
        {
            ?ori :hasAspect
            [ 
                rdf:type :Lijn ;
                :hasValue ?geo1 ;
            ]
        }
        optional # Geen eigen geo, zoek via bPunt, ePunt
        {
            ?ori :hasPart ?bpnt .
            ?bpnt rdf:type :BeginpuntLeiding .
            ?ori :hasPart ?epnt .
            ?epnt rdf:type :EindpuntLeiding .

            ?bpnt :hasConnection
            [ 
              :hasAspect
              [ 
                rdf:type :Punt ;
                :hasValue ?bgeo ;
              ]
            ] .
            bind (strafter(strbefore(str(?bgeo), "</gml:pos>"), "<gml:pos>") as ?bxy)
            ?epnt :hasConnection
            [ 
              :hasAspect
              [ 
                rdf:type :Punt ;
                :hasValue ?egeo ;
              ]
            ] .
            bind (strafter(strbefore(str(?egeo), "</gml:pos>"), "<gml:pos>") as ?exy) # lees inhoud gml:pos

            bind (concat("<gml:LineString xmlns:gml=\"http://www.opengis.net/gml\"><gml:posList srsDimension=\"2\">", str(?bxy), " ", str(?exy), "</gml:posList></gml:LineString>") as ?geo2)
        }
        BIND (COALESCE (?geo1, ?geo2) as ?lijn)
	}
}
