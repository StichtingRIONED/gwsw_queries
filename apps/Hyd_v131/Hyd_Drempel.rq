# Hyd_Drempel.spq
# Gegevens overstortdrempels (20170817)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX : <http://data.gwsw.nl/1.3.1/totaal/>

SELECT ?naam ?bUri ?eUri ?Drempelbreedte ?Drempelniveau ?Afvoercoefficient ?Stromingsrichting

WHERE
{ 
    ?uri sesame:directType :Overstortdrempel .
	
	optional
	{
		?uri rdfs:label ?naam .
	}
	?uri :hasAspect ?ori .
	
    ?ori rdf:type :Onderdeelorientatie ;
         :hasPart [
		  	rdf:type :BeginpuntOnderdeel ;
        	:hasConnection ?bpunt ; ] .
         
        { ?bpunt :isAspectOf ?bUri }
        UNION
        { ?bpunt :isPartOf [ :isAspectOf ?bUri ] . }

	optional
    {
        ?ori :hasPart [
		  	rdf:type :EindpuntOnderdeel ;
        	:hasConnection ?epunt ; ] .
		
        { ?epunt :isAspectOf ?eUri }
        UNION
        { ?epunt :isPartOf [ :isAspectOf ?eUri ] . }
    }
   optional {?uri :hasAspect 
   [
       rdf:type :Drempelbreedte ;
       :hasValue ?Drempelbreedte ;
   ] .}
   optional {?uri :hasAspect 
   [
       rdf:type :Drempelniveau ;
       :hasValue ?Drempelniveau ;
   ] .}
   optional {?uri :hasAspect 
   [
       rdf:type :Afvoercoefficient ;
       :hasValue ?Afvoercoefficient ;
   ] .}
   optional {?uri :hasAspect 
   [
       rdf:type :Stromingsrichting ;
       :hasReference ?Stromingsrichting ;
   ] .}
}
