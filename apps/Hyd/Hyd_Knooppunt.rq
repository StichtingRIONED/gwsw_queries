# Hyd_Knooppunt.spq
# Tbv conversie naar GWSW.hydx (20170901/20190429/20230308/20230719/20230824)
# Op basis topologie-type gwsw:Knooppunt
# Alleen via gwsw:Knooppunt koppelen naar leidingen en onderdelen

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX gwsw: <http://data.gwsw.nl/1.6/totaal/>
PREFIX gwsw: <{{&version}}>

SELECT DISTINCT ?uri ?uriCps ?Stelsel ?naam ?naamComp ?type ?geenPut ?blind ?Punt ?puntCps ?Maaiveldhoogte ?materiaal ?vorm ?breedte ?lengte ?hoogte ?Maaiveldschematisering ?BergendOppervlak ?Grondwaterniveau

WHERE
{ 
  # UNION op alle subtypes van Knooppunt

  {  
     ?ori rdf:type  ?typKnp .
     FILTER (?typKnp IN (gwsw:Bouwwerkorientatie, gwsw:Compartimentorientatie, gwsw:Hulpstukorientatie, gwsw:Putorientatie ))
     ?ori gwsw:isAspectOf ?uri .                  # orientatie moet kenmerk van het fysieke ding zijn
     ?uri rdfs:label      ?naam1 .                # moet een naam hebben

     optional { # markeer de hulpstukken: Verbindingsstuk, Afsluitkap (20230308)
       ?uri rdf:type gwsw:Hulpstuk . 
       BIND ("true" as ?geenPut)
     }
     optional { # markeer de blinde putten (20230309)
       ?uri rdf:type gwsw:BlindePut .
       BIND ("true" as ?blind)
     }
     ?uri sesame:directType ?type .

     FILTER (?type != gwsw:BlindePut) # Uitvoerings-type doet niet mee, functie-type moet er ook zijn (20230309)  
     FILTER (?type != gwsw:VerdektePut) # Uitvoerings-type doet niet mee, functie-type moet er ook zijn (20230309)
  }
  UNION # De aansluitpunten (Uitlaatpunt) zijn geen orientatie, functioneren altijd zelfstandig (ook als aspect-van) (20230719)
  {  
     ?ori rdf:type gwsw:Aansluitpunt . # Zelfstandig knooppunt
     # Dit vervalt (20230719)
     # FILTER NOT EXISTS {?ori gwsw:isAspectOf ?any1 .} # (UitlaatPunt) doen ook mee als ze niet bij een hoofdconstructie horen (20230228)
     BIND (?ori as ?uri) 
     BIND ("true" as ?geenPut)

     # De naam bij een aansluitpunt was niet verplicht, nu wel opgenomen in OroX-protocol (20230719)

     ?uri sesame:directType ?typAansl .
     BIND (str(iri(gwsw:)) AS ?aft) # gebruik de gwsw-IRI
        
# Dit deel voorlopig uitgeschakeld (20230824)
# Als er geen aansluitpunten zijn (deze UNION wordt niet doorlopen !!), loopt de query vast (20230824)
# In de COALESCE() zit de oorzaak?
#     optional {
#      ?uri rdfs:label ?lab1 .
#     }
#     optional { # maak een naam op basis cps
#      ?uri gwsw:isAspectOf [
#           rdfs:label ?labCps ;
#        ] .
#      BIND (CONCAT(str(?labCps), "_", strafter(str(?typAansl), ?aft)) AS ?lab2) # Fabriceer een naam
#     }
#     BIND (CONCAT(strafter(str(?typAansl), ?aft), "_??") AS ?lab3) # Generieke naam (geen probleem, de uri wordt gebruikt als identificatie)
#     BIND (COALESCE(?lab1, ?lab2, ?lab3) as ?naam1)
     
     BIND (strafter(str(?typAansl), ?aft) AS ?naam1) # Generieke naam (geen probleem, de uri wordt gebruikt als identificatie)
  }
  { ?ori gwsw:hasConnection [ rdf:type gwsw:BeginpuntLeiding ;] . }
  UNION
  { ?ori gwsw:hasConnection [ rdf:type gwsw:EindpuntLeiding ;] . }
  UNION
  { ?ori gwsw:hasConnection [ rdf:type gwsw:BeginpuntOnderdeel ;] . }
  UNION
  { ?ori gwsw:hasConnection [ rdf:type gwsw:EindpuntOnderdeel ;] . }
  
  optional # compartimenten hoeven geen punt te hebben 
  {
      ?ori gwsw:hasAspect
    [ 
       rdf:type gwsw:Punt ;
         gwsw:hasValue ?punt1 ;
      ] . 
  }
  optional # zoek evt hoofdconstructie
  {
   ?ori rdf:type gwsw:Compartimentorientatie . # geldt alleen voor compartimenten
   BIND (?naam1 as ?naamComp) .
   ?uri gwsw:isPartOf ?uriCps .
   
   ?uriCps rdfs:label ?naam2 .
   ?uriCps gwsw:hasAspect 
   [
     rdf:type gwsw:Knooppunt ; # Put-ori, bouwwerk-ori, ...
     gwsw:hasAspect 
     [ 
       rdf:type gwsw:Punt ;
       gwsw:hasValue ?punt2 ;
     ] ;
   ] .
  }
  BIND (COALESCE(?punt1, ?punt2) as ?Punt)
  BIND (COALESCE(?naam2, ?naam1) as ?naam)
  BIND (IF(BOUND(?punt1), "", "Ja") AS ?puntCps)
  
  optional 
  {
   ?uri gwsw:hasAspect ?vrm .
   
   { ?vrm rdf:type gwsw:VormPut . }
   UNION
   { ?vrm rdf:type gwsw:VormCompartiment . }
   UNION
   { ?vrm rdf:type gwsw:VormBouwwerk . }
   
   ?vrm gwsw:hasReference ?vorm ;
  }
  optional 
  {
    ?uri gwsw:hasAspect 
    [
      rdf:type gwsw:DiameterPut ;
      gwsw:hasValue ?breedte ; # Diameter als breedte vermelden
    ] .}
  optional 
  {
   ?uri gwsw:hasAspect ?br .
   
   { ?br rdf:type gwsw:BreedtePut . }
   UNION
   { ?br rdf:type gwsw:BreedteCompartiment . }
   UNION
   { ?br rdf:type gwsw:BreedteBouwwerk . }
   
   ?br gwsw:hasValue ?breedte ;
  }
  optional 
  {
   ?uri gwsw:hasAspect ?len .
   
   { ?len rdf:type gwsw:LengtePut . }
   UNION
   { ?len rdf:type gwsw:LengteCompartiment . }
   UNION
   { ?len rdf:type gwsw:LengteBouwwerk . }
   
   ?len gwsw:hasValue ?lengte ;
  }
  optional # Ook hoogte inlezen, voor afleiden put-bob (20230302)
  {
   ?uri gwsw:hasAspect ?hgt .
   
   { ?hgt rdf:type gwsw:HoogtePut . }
   UNION
   { ?hgt rdf:type gwsw:HoogteCompartiment . }
   UNION
   { ?hgt rdf:type gwsw:HoogteBouwwerk . }
   
   ?hgt gwsw:hasValue ?hoogte ;
  }
  
# Gebruik vanaf hier evt het compositie-object

  BIND (IF(BOUND(?uriCps), ?uriCps, ?uri) AS ?uri_) 
  
  optional
  {   
    ?uri_ gwsw:isPartOf 
    [ rdf:type gwsw:Stelsel ;
      rdfs:label ?Stelsel ;
    ] .
  }
  optional 
  {
     ?uri_ gwsw:hasAspect ?oriCps . # Alleen put en bouwwerk verbinden aan maaiveld
    
   { ?oriCps rdf:type gwsw:Putorientatie . }
   UNION
   { ?oriCps rdf:type gwsw:Bouwwerkorientatie . }
   
   optional
     {
     ?oriCps gwsw:hasConnection
     [
       rdf:type gwsw:Maaiveldorientatie ;
       gwsw:hasAspect
       [
       rdf:type gwsw:Maaiveldhoogte ;
       gwsw:hasValue ?Maaiveldhoogte ;
       ];
     ] .
   }
      optional # Grondwaterniveau (20190501)
     {
     ?oriCps gwsw:hasConnection
     [
       rdf:type gwsw:Grondwatermeetpunt ;
       gwsw:hasAspect
       [
       rdf:type gwsw:Grondwaterniveau ;
       gwsw:hasValue ?Grondwaterniveau ;
       ];
     ] .
   }
  }
  optional 
  {
   ?uri_ gwsw:hasAspect ?mat .
   
   { ?mat rdf:type gwsw:MateriaalPut . }
   UNION
   { ?mat rdf:type gwsw:MateriaalBouwwerk . }
   
   ?mat gwsw:hasReference ?materiaal ;
  }
  optional {?uri_ gwsw:hasAspect 
   [
      rdf:type gwsw:Maaiveldschematisering ;
      gwsw:hasReference ?Maaiveldschematisering ;
   ] .}
  optional {?uri_ gwsw:hasAspect 
   [
      rdf:type gwsw:BergendOppervlak ;
      gwsw:hasValue ?BergendOppervlak ;
   ] .}

}
