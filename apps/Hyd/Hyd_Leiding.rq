# Hyd_Leiding.rq (20170901/20190903/20230302)
# Tbv conversie naar GWSW.hydx

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX gwsw: <http://data.gwsw.nl/1.6/totaal/>
PREFIX gwsw: <{{&version}}>

SELECT ?uri ?Stelsel ?naam ?type ?bUri ?eUri ?BobBeginpuntLeiding ?BobEindpuntLeiding ?MateriaalLeiding ?VormLeiding ?BreedteLeiding ?HoogteLeiding ?LengteLeiding ?Wanddikte ?SDR_waarde

WHERE
{ 
    {   ?uri rdf:type gwsw:VrijvervalRioolleiding .
		?uri sesame:directType ?type .
    }
    UNION
    { 	?uri rdf:type gwsw:VrijvervalTransportleiding . 
		BIND("VrijvervalTransportleiding" AS ?type) .
	}
    UNION
    { 	?uri rdf:type gwsw:OpenLeiding . 
		BIND("OpenLeiding" AS ?type) .
	}
   FILTER (!(isBlank(?uri)))
   FILTER (!(isBlank(?type)))
    
   ?uri rdfs:label ?naam .

   optional
   {   
      ?uri gwsw:isPartOf 
      [ rdf:type gwsw:Stelsel ;
        rdfs:label ?Stelsel ;
      ] .
   }
   ?uri gwsw:hasAspect ?ori .
   ?ori rdf:type gwsw:Leidingorientatie .
   
    optional
    {
        ?ori gwsw:hasPart ?bpnt .
        ?bpnt rdf:type gwsw:BeginpuntLeiding .
 
        optional {?bpnt gwsw:hasAspect
        [ 
           rdf:type gwsw:BobBeginpuntLeiding ;
           gwsw:hasValue ?BobBeginpuntLeiding ;
        ] .}
        ?bpnt gwsw:hasConnection ?bpunt .
        
        #{ ?bpunt gwsw:isAspectOf ?bUri }
        #UNION
        #{ ?bpunt gwsw:isPartOf [ gwsw:isAspectOf ?bUri ] . }
        #UNION # Ook de aansluitpunten (UitlaatPunt) doen mee als ze niet bij een hoofdconstructie horen (20230302)
        #{
        #  FILTER NOT EXISTS {?bpunt gwsw:isAspectOf ?any1 .}
        #  FILTER NOT EXISTS {?bpunt gwsw:isPartOf [ gwsw:isAspectOf ?any2 ] . }
        #  BIND (?bpunt as ?bUri) 
        #}

        # Verbinding koppelt aan een knooppunt: orientatie of aansluitpunt (UitlaatPunt) (sync met gebruik gwsw:hasConnection) (20230720)
        {   ?bpunt rdf:type  ?typBpunt .
            FILTER (?typBpunt IN (gwsw:Bouwwerkorientatie, gwsw:Compartimentorientatie, gwsw:Hulpstukorientatie, gwsw:Putorientatie )) 
            ?bpunt gwsw:isAspectOf ?bUri . # orientatie moet kenmerk van het fysieke ding zijn
        }
        UNION # De aansluitpunten (Uitlaatpunt) zijn geen orientatie, functioneren altijd zelfstandig (ook als aspect-van) (20230719)
        {
            ?bpunt rdf:type gwsw:Aansluitpunt . # Zelfstandig knooppunt
            BIND (?bpunt as ?bUri)
        }
	}
	optional
	{
        ?ori gwsw:hasPart ?epnt .
        ?epnt rdf:type gwsw:EindpuntLeiding .

        optional {?epnt gwsw:hasAspect
        [ 
           rdf:type gwsw:BobEindpuntLeiding ;
           gwsw:hasValue ?BobEindpuntLeiding ;
        ] .}
		?epnt gwsw:hasConnection ?epunt .
        
        #{ ?epunt gwsw:isAspectOf ?eUri }
        #UNION
        #{ ?epunt gwsw:isPartOf [ gwsw:isAspectOf ?eUri ] . }
        #UNION # Ook de aansluitpunten (UitlaatPunt) doen mee als ze niet bij een hoofdconstructie horen (20230302)
        #{
        #  FILTER NOT EXISTS {?epunt gwsw:isAspectOf ?any3 .}
        #  FILTER NOT EXISTS {?epunt gwsw:isPartOf [ gwsw:isAspectOf ?any4 ] . }
        #  BIND (?epunt as ?eUri) 
        #}
                
        # Verbinding koppelt aan een knooppunt: orientatie of aansluitpunt (UitlaatPunt) (sync met gebruik gwsw:hasConnection) (20230720)
        {   ?epunt rdf:type  ?typEpunt .
            FILTER (?typEpunt IN (gwsw:Bouwwerkorientatie, gwsw:Compartimentorientatie, gwsw:Hulpstukorientatie, gwsw:Putorientatie )) 
            ?epunt gwsw:isAspectOf ?eUri . # orientatie moet kenmerk van het fysieke ding zijn
        }
        UNION # De aansluitpunten (Uitlaatpunt) zijn geen orientatie, functioneren altijd zelfstandig (ook als aspect-van) (20230719)
        {
            ?epunt rdf:type gwsw:Aansluitpunt . # Zelfstandig knooppunt
            BIND (?epunt as ?eUri)
        }

   }
   optional {?uri gwsw:hasAspect 
   [
       rdf:type gwsw:MateriaalLeiding ;
       gwsw:hasReference ?MateriaalLeiding ;
   ] .}
   optional {?uri gwsw:hasAspect 
   [
       rdf:type gwsw:VormLeiding ;
       gwsw:hasReference ?VormLeiding ;
   ] .}
   optional {?uri gwsw:hasAspect 
   [
       rdf:type gwsw:DiameterLeiding ;
       gwsw:hasValue ?BreedteLeiding ; # Diameter als breedte vermelden
   ] .}
   optional {?uri gwsw:hasAspect 
   [
       rdf:type gwsw:BreedteLeiding ;
       gwsw:hasValue ?BreedteLeiding ;
   ] .}
   optional {?uri gwsw:hasAspect 
   [
       rdf:type gwsw:HoogteLeiding ;
       gwsw:hasValue ?HoogteLeiding ;
   ] .}
   optional {?uri gwsw:hasAspect 
   [
       rdf:type gwsw:LengteLeiding ;
       gwsw:hasValue ?LengteLeiding ;
   ] .}
   optional {?uri gwsw:hasAspect # doet ook mee (20190903)
   [
       rdf:type gwsw:Wanddikte ;
       gwsw:hasValue ?Wanddikte ;
   ] .}
   optional {?uri gwsw:hasAspect # doet ook mee (20190903)
   [
       rdf:type gwsw:SDR_waarde ;
       gwsw:hasValue ?SDR_waarde ;
   ] .}
#   optional {?uri gwsw:hasAspect 
#   [
#       rdf:type gwsw:AfvoerendOppervlak ;
#       gwsw:hasValue ?AfvoerendOppervlak ;
#   ] .}
}
