# Hyd_Persleiding.spq
# Zoek de verbinding met pomp en lozingspunt (- 20190530/20230303)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

#PREFIX : <http://data.gwsw.nl/1.6/totaal/>
PREFIX : <{{&version}}>

SELECT ?naam ?bUri ?bEnd ?eUri ?eEnd

WHERE
{ 
	{ ?lei sesame:directType :Persleiding . }
	UNION
	{ ?lei sesame:directType :Drukleiding . }
	
	?lei rdfs:label ?naam .
	?lei :hasAspect ?ori .
	?ori rdf:type :Leidingorientatie .

    optional
    {
        ?ori :hasPart ?bpnt .
        ?bpnt rdf:type :BeginpuntLeiding .
        ?bpnt :hasConnection ?bPunt .
        
        { ?bPunt :isAspectOf ?bUri }
        UNION
        { ?bPunt :isPartOf [ :isAspectOf ?bUri ] . }
        
        optional { # markeer het eindpunt (20230303)
            {?bUri rdf:type :Lozingsput . }
            UNION
            {?bUri rdf:type :Inspectieput . }
            BIND ("true" as ?bEnd)
        }
        # ?bUri sesame:directType ?bType .
	}
 	optional
	{
        ?ori :hasPart ?epnt .
        ?epnt rdf:type :EindpuntLeiding .
		?epnt :hasConnection ?ePunt .
        
        { ?ePunt :isAspectOf ?eUri }
        UNION
        { ?ePunt :isPartOf [ :isAspectOf ?eUri ] . }
        
        optional { # markeer het eindpunt (20230303)
            {?eUri rdf:type :Lozingsput . }
            UNION
            {?eUri rdf:type :Inspectieput . }
            BIND ("true" as ?eEnd)
        }
        # ?eUri sesame:directType ?eType .
	}
}
